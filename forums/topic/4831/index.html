<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>Newbie's First Program - Comments Please - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=4831" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=3">MAIN</a> &raquo; <a href="../?id=4831">Newbie's First Program - Comments Please</a></p>
   <div class="post" id="post-33861">
    <div class="subject"><a href="#post-33861">Newbie's First Program - Comments Please</a></div>
    <div class="body">First, thanks SO MUCH to hutch, Thomas, and especially NaN for helping get over the crux of starting with assembly.  I have written my first original assembly program!<br /><br />Okay, by original I don't mean it's the first time the world has ever seen this--it just prints an ASCII chart.  I mean original in that I wrote all the code myself rather than modifying a tutorial.  Anyway, it's pretty trivial, about 50 lines of code, but still, I'm rather pleased.  :)<br /><br />Comments?  Thoughts?  How could I make this better, smaller, faster, more compact, more readable, etc?<br /><br />I'm especially concerned about the mul instruction commented out below.  Masm claims that the comma is a syntax error.  What's up with that?<br /><br />Thank you all in advance for comments.  Code attached below, sorry for it making this post so large.<br /><br />-Chalain<br />&quot;We all begin life naked, covered in blood and screaming.  But if we live right, it doesn't have to stop there.&quot;  -- Robert A. Heinlein<br /><br /><pre><code>;=======================================================================<br />;  File&#58; ascii.asm<br />;  Desc&#58; Prints the ASCII chart as a 16x16 grid with hex labels.  My<br />;   first &#40;original&#41; assembly program.  <br />;  Auth&#58; Chalain<br />;  Date&#58; 04/17/2002<br />;-----------------------------------------------------------------------<br />; Notes&#58;<br />;	Win32 Assembly *console* project.  To compile and link with masm32,<br />;		use BLDALLC.BAT, or compile by hand like so&#58;<br />;<br />;	ml /c /coff ascii.asm<br />;	link /SUBSYSTEM&#58;CONSOLE ascii.obj<br />;<br />;=======================================================================<br /><br />;-----------------------------------------------------------------------<br />; DIRECTIVES<br />;-----------------------------------------------------------------------<br />.386<br />.model flat, stdcall<br />option casemap&#58;none<br /><br />;-----------------------------------------------------------------------<br />; INCLUDES<br />;-----------------------------------------------------------------------<br />include \masm32\include\Windows.inc<br />include \masm32\include\masm32.inc<br />include \masm32\include\kernel32.inc<br />include \masm32\include\user32.inc<br /><br />includelib \masm32\lib\user32.lib<br />includelib \masm32\lib\masm32.lib<br />includelib \masm32\lib\kernel32.lib<br /><br />;-----------------------------------------------------------------------<br />; .DATA<br />;-----------------------------------------------------------------------<br />.data<br /><br />; ascii-art caption text<br />AsciiArtTopBar      db &quot;  &quot;,218, 33 dup&#40;196&#41;,191,13,10,0<br />AsciiArtTopCaption  db &quot;  &quot;,179<br />                    db &quot; 0 1 2 3 4 5 6 7 8 9 A B C D E F &quot;<br />                    db 179, 13, 10, 0<br />AsciiArtTopBar2     db 218,196,197, 33 dup&#40;196&#41;,180,13,10,0<br />AsciiArtBottomBar   db 192,196,193, 33 dup&#40;196&#41;, 217, 13, 10, 0<br />AsciiArtVert        db 179<br /><br />ColumnFormat        db &quot;%c%X%c &quot;,0      ; formats row headers<br />CharFormat          db &quot;%c &quot;,0          ; formats the actual chars<br />EndColumnText       db 179,13,10,0      ; | + cr + lf<br /><br />;----------------------------------------------------------------------<br />; .DATA?<br />;----------------------------------------------------------------------<br />.data?<br />x                   dd ?<br />y                   dd ?<br />Ascii               dd ?<br />Buffer              db 10 dup&#40;?&#41;<br /><br />;----------------------------------------------------------------------<br />; .CODE<br />;----------------------------------------------------------------------<br />.code<br />start&#58;<br />    invoke StdOut, addr AsciiArtTopBar<br />    invoke StdOut, addr AsciiArtTopCaption<br />    invoke StdOut, addr AsciiArtTopBar2<br /><br />    mov y,0<br />    .WHILE y&lt;16<br />        mov x,0<br />        invoke wsprintf, addr Buffer, addr ColumnFormat, \<br />                dword ptr AsciiArtVert, y, dword ptr AsciiArtVert<br />        invoke StdOut, addr Buffer<br />        .WHILE x&lt;16<br />            mov eax, y<br />;           mul eax, 16	; WTF?  This is a syntax error?<br />            shl eax, 4	; shl is probably faster anyway<br />            add eax, x<br />            mov Ascii, eax<br />            ; Don't print Ascii 0, 7-10 or 13--they'll trash our output<br />            .IF Ascii == 0 || &#40; Ascii &gt;= 7 &amp;&amp; Ascii &lt;= 10 &#41; || Ascii == 13<br />                mov Ascii, 20h<br />            .ENDIF<br />            invoke wsprintf, addr Buffer, addr CharFormat, \<br />                        dword ptr Ascii<br />            invoke StdOut, addr Buffer<br />            inc x<br />        .ENDW<br />        invoke StdOut, addr EndColumnText<br />        inc y<br />    .ENDW<br /><br />    invoke StdOut, addr AsciiArtBottomBar<br />    invoke ExitProcess, 0<br />end start</code></pre></div>
    <div class="meta">Posted on 2002-04-17 14:28:25 by Chalain</div>
   </div>
   <div class="post" id="post-33865">
    <div class="subject"><a href="#post-33865">Re: Newbie's First Program - Comments Please</a></div>
    <div class="body"><div class="quote"><br />            mov eax, y<br />;           mul eax, 16	; WTF?  This is a syntax error?<br />            shl eax, 4	; shl is probably faster anyway</div><br /><br />Heheh, its easy to think this way, as practically all the INTEL ASM commands are formatted dest, source.  However, with multiply and divide, there are special *assumed* regesters that can only be used.  In doing so, there is only one param, the &quot;multiply by&quot; and the &quot;divide by&quot; source.<br /><br />I didn't make it all to clear i guess in my simple.asm.  <br /><br />Preceeding the multiply command is me setting up EAX with a number.  This is the source number, mul assumes that EAX will be used for multiplying.<br /><br />mov eax, 3<br />mov ecx, 4<br />mul ecx<br /><br />is:<br /><br />eax = 3<br />ecx = 4<br />edx:eax == eax * ecx<br /><br />Also note, that the edx:eax is ment to represent that the result is 64 bits long, and distributed between the two pernament output registers.   In simple, i sugessted that im hoping the input # was relatively small, doing so insured that there would not be any high order bits in edx (since eax can hold up to 4 billion as is).<br /><br />I sugest you get an OP-Code reference to help you on your way, there is a nifty HelpPC 21 program i like to use (use board search to find a thread).  As well on my web site, i have .hlp files that also document the asm commands (in the Help section).  Click the www button below if interested.<br /><br />Enjoy..<br />:alright:<br />NaN</div>
    <div class="meta">Posted on 2002-04-17 14:42:15 by NaN</div>
   </div>
   <div class="post" id="post-33866">
    <div class="subject"><a href="#post-33866">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body">Hi Chalain, glad to see you're learning quick :)<br />Here are a few tips:<br /><br />One sidenote: Optimisations for this code won't make it really faster, as the speed gained by it is nothing compared to the time it takes to print something to the console. I will point them out anyway, so you can learn from it, but keep this in mind.<br /><br />You are now using a variable (y) for the loop, you can use a register instead to make it faster. However you will need to use a non-volatile register (esi, edi or ebx), as the others (eax, ecx, edx etc) are destroyed by the API functions. Only esi, edi and ebx (ebp and esp as well of course) are preserved. <br /><br />You can't 'mul eax, 16' as the mul opcode does not allow immediate operands (a number). The signed multiply (imul) opcode does allow this, but a simple solution is to first put 16 in a register, and then multiplying by eax.<br />Also, mul only takes one operand. It always multiplies EAX by this operand, so you only have to specify the other operand, like this:<br /><pre><code><br />mul ecx<br />or<br />mul variable<br /></code></pre><br /><br />In your case:<br /><pre><code><br />mov ecx, 16 ; put 16 in ecx<br />mul ecx ;mul eax by ecx<br /></code></pre><br /><br />But as you already commented, use shl eax, 4, it's much faster. For divisions, you can use SHR in the same way (shr eax, 3 is divide by 8)<br /><br />You do not need to put eax in Ascii first, just use eax directly:<br /><pre><code><br />.IF eax== 0 || &#40; eax&gt;= 7 &amp;&amp; eax&lt;= 10 &#41; || eax== 13<br />                mov eax, 20h<br />.ENDIF<br />invoke wsprintf, addr Buffer, addr CharFormat, eax<br /></code></pre><br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-17 14:50:21 by Thomas</div>
   </div>
   <div class="post" id="post-33867">
    <div class="subject"><a href="#post-33867">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body"><strong>NaN</strong>: you beat me :grin:<br /><br />Here's some more on the range checking:<br />The .IF statement in masm can be very useful for code readability, but complex statements can be done better manually. It's also good to know how things work internally so here you go:<br /><br /><pre><code><br />or	eax, eax	;fast way to see if eax=0<br />jz	_charnotok	;if zero, jump<br /><br />; char is not zero here<br /><br />cmp 	eax, 13		;is eax 13?<br />ja	_charok		;if above, char is okay<br />je	_charnotok	;if equal, char is not okay<br /><br />; char is less than 13 here<br /><br />cmp     eax, 10		;char &gt; 10<br />ja	_charok		;&gt;10 &#40;and &lt;13&#41; so okay<br />cmp	eax, 7<br />jb	_charok		;&lt;7 &#40;and &gt;0&#41; so okay<br />;jmp	_charnotok	;jump not necessary, fall through<br />_charnotok&#58;<br />mov	eax, &quot; &quot;<br />_charok&#58;<br /></code></pre><br />(untested but should work)<br /><br />Some of the optimisation gurus here have found more clever ways to do range checking, this is just the basic conversion to help you understand what's going on. masm will produce somewhat different code..<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-17 14:57:53 by Thomas</div>
   </div>
   <div class="post" id="post-33870">
    <div class="subject"><a href="#post-33870">Re: Newbie's First Program - Comments Please</a></div>
    <div class="body"><div class="quote"><br />.data<br />...<br />AsciiArtVert        db 179<br />...<br /><br />.data?<br />x                   dd ?<br />y                   dd ?<br />Ascii               dd ?<br />Buffer              db 10 dup(?)<br /><br />.code<br /> ...<br />        invoke wsprintf, addr Buffer, addr ColumnFormat, \<br />                dword ptr AsciiArtVert, y, dword ptr AsciiArtVert<br /> ...<br />        invoke wsprintf, addr Buffer, addr CharFormat, \<br />                dword ptr Ascii<br /></div><br />I suspect you ran into some problems here, as im wondering why you placed DWORD PTR's (its becoming clear to me your no noobie by the way, you just need to get the facts straight :) ).<br /><br />I didnt mention the <strong>dd</strong> but its seems you already figured that out. However, you should also have <strong>AsciiArtVert</strong> defined as a double (instead of a byte).  Then you dont need to write any DWORD PTR's, just place the variables in the param list:<br /><br /><strong>invoke wsprintf, addr Buffer, addr ColumnFormat, AsciiArtVert, AsciiArtVert</strong><br /><br />(( I didnt compile this, and would be forced to eat my shoes if i made an error :grin: ))<br /><br /><br />Also, to introduce more *fun* stuff.  To be a real Jedi of MASM, you should begin to learn (slowly) the fine are of MACRO writting.  (M = Macro)ASM :grin: .  They can make the code more readible and give you less typing overal.<br /><br />NOTE: Macro's are NOT functions, they are just &quot;code stamped into place&quot;.  So, this means many uses will increase file size!<br /><br /><u>An Example:</u>  Lets make a MACRO function for the wsprintf/stdout pair.  <br /><pre><code>PRINTF &#91;b&#93;MACRO&#91;/b&#93; StingData&#91;b&#93;&#58;REQ&#91;/b&#93;, args&#91;b&#93;&#58;VARARG&#91;/b&#93;<br />  <br />     IFNDEF __PRINTF_MACRO         ;; Precompiler IF statement<br />                                   ;; If NOT DEFINED &quot; &quot;<br />          __PRINTF_MACRO equ 1     ;; Then Define IT<br />          <br />          .data?                   ;; Then make a special BUFFER!<br />                                   ;; in the .data? section. &#40;Masm will<br />                                   ;; catch this in its first pass, and<br />                                   ;; move it out of the code section and<br />                                   ;; into the segment specified, so its ok!&#41;<br />          PF_BUFFER db 128 dup&#40;?&#41;  ;; Define a buffer<br />          <br />          .code                    ;; Tell MASM that code now resumes!<br />     ENDIF                         ;; END of Precompiler IF<br />     <br />     ;;   Now The MACRO can assume that PF_BUFFER is defined!<br />     ;;   Lets make the code to be stamped!<br />     <br />     IFNB args                     ;; PRECOMPILE THOUGHT&#58; If args is NOT Blank<br />          <br />          ;; Then &quot;stamp&quot; code into place with extra args!<br />          invoke wsprintf, &#91;b&#93;addr PF_BUFFER&#91;/b&#93;, &#91;b&#93;addr StringData&#91;/b&#93;, &#91;b&#93;&#91;u&#93;args&#91;/u&#93;&#91;/b&#93;<br />     <br />     ELSE                          ;; Args IS BLANK!<br />     <br />          invoke wsprintf, &#91;b&#93;addr PF_BUFFER&#91;/b&#93;, &#91;b&#93;addr StringData&#91;/b&#93;<br />     <br />     ENDIF                         ;; END OF IF<br />     <br />     ;;   now Lets display the BUffer!<br />     invoke StdOut, &#91;b&#93;addr PF_BUFFER&#91;/b&#93;<br />     <br />ENDM                               ;; END Macro!<br /></code></pre><br /><br />Its uses can be (in your case):<br /><br /><strong>PRINTF ColumnFormat, AsciiArtVert, AsciiArtVert</strong><br /><strong>PRINTF CharFormat, Ascii</strong><br /><br /><br />Again, im in a bit of a hurry, so i didnt compile and test it, but it should work!  If not lemme know! (( BTW, the expanded Macro code will look the same as your original, except an extra buffer reserved of just this macro is also defined in your code ))<br /><br />Hope this is clear to you... <br />Enjoy!<br />:alright:<br />NaN</div>
    <div class="meta">Posted on 2002-04-17 15:20:32 by NaN</div>
   </div>
   <div class="post" id="post-33875">
    <div class="subject"><a href="#post-33875">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body">Here's a total different version of your program. It builds the whole table (except for the top and bottom bars) in the buffer first without any APIs and then outputs the whole buffer.<br /><pre><code><br />;-----------------------------------------------------------------------<br />; .DATA<br />;-----------------------------------------------------------------------<br />.data<br /><br />; ascii-art caption text<br />AsciiArtTop         db &quot;  &quot;,218, 33 dup&#40;196&#41;,191,13,10<br />                    db &quot;  &quot;,179<br />                    db &quot; 0 1 2 3 4 5 6 7 8 9 A B C D E F &quot;<br />                    db 179, 13, 10<br />                    db 218,196,197, 33 dup&#40;196&#41;,180,13,10,0<br />AsciiArtBottom      db 192,196,193, 33 dup&#40;196&#41;, 217, 13, 10, 0<br /><br />;----------------------------------------------------------------------<br />; .DATA?<br />;----------------------------------------------------------------------<br />.data?<br />Buffer              db 640 dup&#40;?&#41;<br /><br />;----------------------------------------------------------------------<br />; .CODE<br />;----------------------------------------------------------------------<br />.code<br />start&#58;<br />    invoke StdOut, addr AsciiArtTop<br /><br />    mov     edi, offset Buffer      ;edi points to buffer<br />    mov     edx, 20B330B3h          ;&quot;|0| &quot; as little endian dword = 20B330B3h<br />    mov     ebx, 0A0D20B3h          ;&quot;| &quot;, CR,LF as little endian dword = 0A0D20B3h<br />    mov     eax, 20012000h          ;0,&quot; &quot;,1,&quot; &quot; as little endian dword = 20012000h<br />                                    ;eax will hold two output ASCII codes at a time,<br />                                    ;seperated by spaces<br />    mov     esi, 16<br />    _yLoop&#58;<br />        add     edi, 4<br />        mov     ecx, 16/2       ; 16 chars, 2 chars at a time<br />        mov     &#91;edi-4&#93;, edx    ; store left part &quot;|X|  &quot;, -4 is <br />                                ; because of the 4 added to edi<br />        _xLoop&#58;<br />            mov     &#91;edi&#93;, eax<br />            add     eax, 00020002h  ;add 2 to both ASCII codes<br />            add     edi, 4      <br />            <br />        dec     ecx         ; x--<br />        jnz     _xLoop      ; if decrease did not result in zero, loop x<br />        <br />        inc     dh          ;increase row number digit<br />        <br />        mov     &#91;edi&#93;, ebx  ;add decoration at end of line<br />        <br />        ; check if row number digit is one greater than &quot;9&quot;. if yes,<br />        ; it should be converted to &quot;A&quot;.<br />        cmp     dh, &quot;9&quot;+1   ; If it's this value, it should be &quot;A&quot;<br />        jne     @F          ; if not equal, just go on<br />        ;Note&#58; @F means jump to the next @@&#58; label,<br />        ; @B means jump to the previous @@&#58; label<br />        ; You can have as many @@&#58; labels as you want<br />        <br />        ; otherwise, make it &quot;A&quot;&#58;<br />        mov     dh, &quot;A&quot;<br />        @@&#58;<br />        <br />        ;Note, for pentium pro processors, the above AH check can be optimized <br />        ;with the new CMOV instruction&#58;<br />        ; cmp   dh, &quot;9&quot;+1   ;compare DH with &quot;9&quot;+1<br />        ; cmove dh, &quot;A&quot;     ;if equal &#40;cmovE&#41;, move &quot;A&quot; into DH<br />        <br />        add     edi, 4 ;move to next dword<br />        <br />    dec     esi         ; y--<br />    jnz     _yLoop      ; if decrease did not result in zero, loop y<br />    <br />    mov     byte ptr &#91;edi&#93;, 0   ;add final null-terminator<br />    <br />    ; blank out invalid chars&#58;<br />    mov     eax, &quot;    &quot;<br />    mov     byte ptr &#91;Buffer+4&#93;,  al    ;put space at place of the zero code<br />    mov     byte ptr &#91;Buffer+30&#93;, al    ;blank out value D<br />    mov     dword ptr &#91;Buffer+18&#93;, eax  ;blank out values 7 &amp; 8<br />    mov     dword ptr &#91;Buffer+22&#93;, eax  ;blank out values 9 &amp; A<br />    <br />    <br />    invoke StdOut, addr Buffer<br /><br />    invoke StdOut, addr AsciiArtBottom<br />    invoke ExitProcess, 0<br />end start<br /></code></pre><br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-17 16:17:51 by Thomas</div>
   </div>
   <div class="post" id="post-33878">
    <div class="subject"><a href="#post-33878">Re: Newbie's First Program - Comments Please</a></div>
    <div class="body"><div class="quote"><br /><br />I suspect you ran into some problems here, as im wondering why you placed DWORD PTR's (its becoming clear to me your no noobie by the way, you just need to get the facts straight :) ).</div><br /><br />Hee hee!  Blush, thank you.  Truth be told, I pay the rent with C, C++, and Python programming.  I've been programming for about 17 years, and have cursed myself roundly in the past for not being able to read disassembles of my own code.  So yeah, I sort of know <em>what</em> I want to accomplish with assembly--for example, windows API assembly is fairly clear to me, b/c I grok the API.  I just have no idea <em>how</em> to go about it.  :)<br /><br />The dword ptr thing was indeed put there because it was causing grief.  In the format of &quot;%c%X%c &quot;, which spits out |1| , |2| , ..., |F| , it was spitting out<br /><br />|0|<br />|1000|<br />|2000|<br />|3000|<br /><br />Whee!  I'm not sure entirely what the deal here was, I'd love a better explanation if someone has one.  My hypothesis is that somehow AsciiArtVert was being treated as a dword parameter, but when it was popped off the stack the stack pointer was indexed one byte (the correct size of AsciiArtVert).  This leaves three zeroes in memory in front of y which effectively multiplies it by 1000h in little-endian.  The thing that baffles me is that the second | still came out right.  *shakes head*<br /><br /><div class="quote"><strong>However, you should also have <strong>AsciiArtVert</strong> defined as a double (instead of a byte).</div><br /><br />Right, that would have the same effect.  So... somebody somewhere is expecting AsciiArtVert to be a dword.  Who, and why?  Is VARARGS exclusively a list of pointers (hence, dwords)?  No, that doesn't make any sense because it's using the contents, not the address... *sigh* I'm rambling.  Thoughts?<br /><br /><div class="quote"><strong>Also, to introduce more *fun* stuff.  To be a real Jedi of MASM, you should begin to learn (slowly) the fine are of MACRO writting.  (M = Macro)ASM :grin: .  They can make the code more readible and give you less typing overal.<br />&lt;snip&gt;<br />PRINTF ColumnFormat, AsciiArtVert, AsciiArtVert<br />PRINTF CharFormat, Ascii</div><br /><br />Awesome!  Um, it doesn't work.  :confused:  I get this error message:<br /><br />ascii.asm(132) : error A2051: text item required<br /> PRINTF(18): Macro Called From<br />  ascii.asm(132): Main Line Code<br /><br />If I'm reading this correctly (I've reformatted your macro a bit), the problem is with &quot;INFB args&quot;.<br /><br />Oh, which brings me to my NEXT question.  Is there a MASM manual out there, that explains the bizarre gobbledygook of the assembler itself?  Like what the heck is @@: I keep seeing, etc?<br /><br />Thanks!<br /><br />-Chalain<br />IFNB Brain<br />; Note: this code never compiles<br />ENDIF</div>
    <div class="meta">Posted on 2002-04-17 16:31:59 by Chalain</div>
   </div>
   <div class="post" id="post-33880">
    <div class="subject"><a href="#post-33880">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body"><strong>Chalain:</strong> you probably post your reply at the same time as I did. I've explained about the @@ in my code.<br />@@ is a special label which you can use multiple times:<br /><br /><pre><code><br />mov eax, ecx<br />@@&#58;<br />mov edx, eax<br />inc edx<br />@@&#58;<br />add eax, ecx<br /></code></pre><br /><br />To jump to these labels, two special jump labels are provided: @B and @F. The first jumps back (hence the B :) ) to the previous @@ in your code. The @F jumps forward to the next @@ in your code. <br />It's very useful for jumps over small pieces of code so you won't have to come up with unique names all the time.<br /><br />The masm reference was available on bitRAKE's site but I don't know if it's still there.. there's an online version as well, do a search on this board on &quot;MASM reference online&quot; and you will find a post by bitRAKE with an URL to the online reference. <br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-17 16:37:20 by Thomas</div>
   </div>
   <div class="post" id="post-33881">
    <div class="subject"><a href="#post-33881">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body"><div class="quote"><br /><pre><code><br />        ;Note&#58; @F means jump to the next @@&#58; label,<br />        ; @B means jump to the previous @@&#58; label<br />        ; You can have as many @@&#58; labels as you want<br /></code></pre></div><br /><br />Ah!  Okay.  Grr, must... find... manuals...  :)<br /><br />Thanks!<br /><br />-Chalain<br />Ask and ye shall discover that ye have already received...</div>
    <div class="meta">Posted on 2002-04-17 16:40:52 by Chalain</div>
   </div>
   <div class="post" id="post-33882">
    <div class="subject"><a href="#post-33882">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body"><div class="quote"><br /><strong>Chalain:</strong> you probably post your reply at the same time as I did.</div><br /><br />AHHH!!! Get out of my head!!!<br /><br />:grin:<br /><br />-Chalain</div>
    <div class="meta">Posted on 2002-04-17 16:42:06 by Chalain</div>
   </div>
   <div class="post" id="post-33883">
    <div class="subject"><a href="#post-33883">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body">:grin:<br /><br /><div class="quote">....Truth be told, I pay the rent with C, C++, and Python programming.....</div><br />Do you have visual C? If you have it, you can use it's nice debugger to trace through your asm code (as you typed it, not the disassembly), observing what happens to the registers and memory.<br />Just add /Zi and /Zd to ml command line, and /debug /debugtype:CV to the link command line. You can use int 3 (one of the rare occations you may use an interrupt in windows :) ) in your code to break, as interrupt 3 is the breakpoint interrupt. Visual C will kick in then if you choose to debug the program.<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-17 16:46:46 by Thomas</div>
   </div>
   <div class="post" id="post-33885">
    <div class="subject"><a href="#post-33885">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body"><div class="quote"><br />Do you have visual C?</div><br /><br />AWESOME!  Yeah, I have VC, and I actually use DevStoo as my text editor for just about everything--I've got it macro-ified beyond belief.  Heh, I've actually used this bit of C code in the past:<br /><br /><pre><code><br />#define DEBUGBREAK __asm &#123; int 03h; &#125;<br /></code></pre><br /><br />It's exactly the same thing.  I use it to debug remote .DLLs and other objects that stubbornly refuse to be launched with a cheerful F5 in DevStoo.  :-)<br /><br />I'll try the debugging switches, that'll make life a LOT more simple.  (*sniff* I used to have a geniune, honest-to-goodness legal copy of SoftICE at my last job.  VERY cranky as debuggers go, but man it could do ANYTHING.)<br /><br />Cheers,<br /><br />-Chalain<br />Timeout while trying to think up a funny sig</div>
    <div class="meta">Posted on 2002-04-17 16:56:33 by Chalain</div>
   </div>
   <div class="post" id="post-33900">
    <div class="subject"><a href="#post-33900">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body">The fix for the IFNB is:<br /><br /><strong>IFNB &lt;args&gt;  </strong><br /><br />Forgot to wrap it in quotes ( Macro's assume &lt; and &gt; to be string start and stop points )<br /><br />As well, almost everything in API's are DWORD in length.  Pointers for sure, but BYTES and WORDS are actually passed with 32bit alignments (DWORDS); the extras are simply ignored.  This keeps the instruction pipes running smoother when calling API's...<br /><br />Also, there are many sources of MASM guides, but an online version is at :<br /><br /><a target="_blank" href="http://web.sau.edu/LillisKevinM/csci240/masmdocs/">http://web.sau.edu/LillisKevinM/csci240/masmdocs/</a><br /><br />I suggest to rip it down to your hard disk...<br /><br />As well there is a MASM refernce posted reciently by ThoughtCriminal:<br /><br /><a target="_blank" href="http://www.asmcommunity.net/board/index.php?topic=4739&amp;highlight=masm+OR+guide">http://www.asmcommunity.net/board/index.php?topic=4739&amp;highlight=masm+OR+guide</a><br /><br />Get it, its very handy...  As well there is a lengthy tut about STRUCT too.. may be of some interest to you...<br /><br />Enjoy..<br />:alright:<br />NaN</div>
    <div class="meta">Posted on 2002-04-17 21:56:51 by NaN</div>
   </div>
   <div class="post" id="post-33908">
    <div class="subject"><a href="#post-33908">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body"><div class="quote"><br />The fix for the IFNB is:<br /><br />IFNB &lt;args&gt;<br /></div><br /><br />Perfect!  Heh, this has been very educational.  When I was originally debugging your macro, I thought the IFNDEF/ENDIF pair was misaligned--in C, the entire header file is #ifdef'ed out, so I moved the ENDIF to the end of the macro.<br /><br />Once I put in the &lt;args&gt; reference, it worked--but the characters weren't being printed.  Just the columns.  Reinspection showed that only the .data chunk is what we wanted to IFDEF away, and that the macro has to be expanded anew each time it is called.  You already knew this, I'm sure... but hey, another lesson learned!<br /><br />Lots of thanks, Thomas and NaN... I feel like I can &quot;play&quot; with assembly now and see the results of my fiddling, and I'm sure it won't be long before I move on to &quot;real&quot; windows apps with much bolstered confidence.<br /><br />Thanks tons!<br /><br />-Chalain<br />I'm feeling very surreal today... or... <em>AM I?!?</em></div>
    <div class="meta">Posted on 2002-04-17 22:58:12 by Chalain</div>
   </div>
   <div class="post" id="post-33911">
    <div class="subject"><a href="#post-33911">Main::Newbie's First Program - Comments Please</a></div>
    <div class="body">Chalain,<br /><br />I am pleased to see that you have some code up and going, translating from your previous experience seems to be working well.<br /><br />I had a comment on the LOCAL or ANONYMOUS labels @@: etc ...<br /><br />They may look unusual but when you start coding algorithms, you will truly appreciate their capacity instead of endlessly inventing new label names. They are mainly for dealing with close range loop code labels where unique names are not all that intuitive but where jump back to last @@: with jmp @B or jump forward to the next @@: with jmp @F is really obvious and clear.<br /><br />Keep up the good work.<br /><br />Regards,<br /><br /><a href="mailto:hutch@movsd.com">hutch@movsd.com</a></div>
    <div class="meta">Posted on 2002-04-17 23:57:00 by hutch--</div>
   </div>
  </div>
 </body>
</html>