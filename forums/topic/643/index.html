<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>Edit Control Subclassing,what am i doing wrong - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=643" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=5">Algorithms &amp; Source Code</a> &raquo; <a href="../?id=643">Edit Control Subclassing,what am i doing wrong</a></p>
   <div class="post" id="post-3850">
    <div class="subject"><a href="#post-3850">Edit Control Subclassing,what am i doing wrong</a></div>
    <div class="body">I have an edit control subclassed to allow only hex digits.<br />The problem is when i paste something to it gets wrong result.<br />And also what should I correct to allow pasting through ctrl+v.<br />here is my code (lame?)<br /><br />EditWndProc PROC hEdit:DWORD,uMsg:DWORD,wParam:DWORD,lParam:DWORD<br />    LOCAL BUFF[128]:BYTE<br />    LOCAL tlen:DWORD<br />	.if uMsg==WM_CHAR<br />		mov eax,wParam<br />      	.if al&gt;=&quot;a&quot; &amp;&amp; al&lt;=&quot;f&quot;<br />			sub al,20h<br />		.endif<br />		.if (al&gt;=&quot;0&quot; &amp;&amp; al&lt;=&quot;9&quot;) || (al&gt;=&quot;A&quot; &amp;&amp; al&lt;=&quot;F&quot;) || al==VK_BACK<br />			invoke CallWindowProc,OldEdProc,hEdit,uMsg,eax,lParam<br />			ret<br />		.endif<br />  	.elseif uMsg==WM_PASTE<br />            mov tlen,0<br /> 		invoke CallWindowProc,OldEdProc,hEdit,uMsg,wParam,lParam<br />            invoke GetWindowText,hEdit,addr BUFF,127<br />            invoke SetWindowText,hEdit,addr tlen<br />            push    esi<br />            lea     esi,BUFF<br />            .while  tlen&lt;127<br />                lodsb<br />                invoke  SendMessage,hEdit,WM_CHAR,eax,0<br />                inc tlen<br />            .endw<br />            pop esi<br /> 		ret<br />	.else<br />		invoke CallWindowProc,OldEdProc,hEdit,uMsg,wParam,lParam<br />		ret<br />	.endif<br />	xor eax,eax<br />	ret<br />EditWndProc endp</div>
    <div class="meta">Posted on 2001-08-15 23:47:59 by Yuk</div>
   </div>
   <div class="post" id="post-3880">
    <div class="subject"><a href="#post-3880">Edit Control Subclassing,what am i doing wrong</a></div>
    <div class="body">Try changing the WM_PASTE code to this:<br /><pre><code><br />    .elseif uMsg==WM_PASTE<br />        invoke CallWindowProc, OldWndProc, hEdit, uMsg, wParam, lParam<br />        invoke GetWindowText, hEdit, addr BUFF, 127<br />        mov tlen, eax<br />        invoke SetWindowText, hEdit, NULL<br />        <br />        push esi<br />        xor esi, esi<br />@@&#58;     xor eax, eax<br />        mov al, BUFF&#91;esi&#93;<br />        invoke SendMessage, hEdit, WM_CHAR, eax, 0<br />        inc esi<br />        cmp esi, tlen<br />        jnz @B<br /><br />        pop esi <br /></code></pre><br />I'm not sure what your problem was. I re-wrote the while loop in a way I understood, and it works!<br />It also only adds characters up to the length of the text. This may have been part of the problem you were having, but I can't be sure....<br /><br />I may look at the original code some more if I've got time.<br /><br />Mirno</div>
    <div class="meta">Posted on 2001-08-16 06:43:11 by Mirno</div>
   </div>
   <div class="post" id="post-3881">
    <div class="subject"><a href="#post-3881">Edit Control Subclassing,what am i doing wrong</a></div>
    <div class="body">Doh!<br />I am soooo stupid!<br /><br />Yes the scanning the whole buffer is the problem!<br /><br />When you declare the buffer it is on the stack.<br />The contents of this buffer (at initialisation) are whatever was on the stack previously (basically a whole lot of rubbish). Because you scan the entire 128 bytes of the buffer (no matter how long the string is), you go into this unknown stuff in the buffer. Some of it happens to be in the range allowed by your WM_CHAR stuff, and so its added to the edit box.<br /><br />To avoid this problem using your own code (rather than mine which also does the job, but in a different way), you need to leave the WHILE loop when you read the NULL terminator of the string.<br /><pre><code><br />        .while tlen &lt; 127<br />            lodsb<br />            .BREAK .IF al == 0<br />            invoke SendMessage, hEdit, WM_CHAR, eax, 0<br />            inc tlen <br />        .endw<br /></code></pre><br /><br />The extra line &quot;.BREAK .IF al == 0&quot; performs an extra comparison, and if it is the null terminator will exit your while loop early.<br /><br />Mirno</div>
    <div class="meta">Posted on 2001-08-16 06:59:23 by Mirno</div>
   </div>
   <div class="post" id="post-3884">
    <div class="subject"><a href="#post-3884">thankyou</a></div>
    <div class="body">Thank you Mirno.<br />I see. I will try your code. <br />Btw, My EditControl has a fixed length of 16.</div>
    <div class="meta">Posted on 2001-08-16 07:33:13 by Yuk</div>
   </div>
   <div class="post" id="post-3889">
    <div class="subject"><a href="#post-3889">Edit Control Subclassing,what am i doing wrong</a></div>
    <div class="body">In WM_PASTE<br />I would open buffer and check in it if the string all characters<br />are valid.<br />If not - just exit subproc.<br />Else call OldProc</div>
    <div class="meta">Posted on 2001-08-16 08:08:36 by The Svin</div>
   </div>
   <div class="post" id="post-3895">
    <div class="subject"><a href="#post-3895">Edit Control Subclassing,what am i doing wrong</a></div>
    <div class="body">You can also do this without the buffer (or the tlen because I used esi &amp; edi instead).<br /><br />This code does the following:<br />1) Check the clipboard format (make sure its text)<br />2) Open the clipboard<br />3) Gets the data from the clipboard<br />4) Adds it character by character to the original text.<br /><br /><pre><code><br />.ELSEIF uMsg == WM_PASTE<br />    invoke IsClipboardFormatAvailable, CF_TEXT<br />    or eax, eax<br />    jnz @F<br />    xor eax, eax<br />    ret<br /><br />@@&#58; invoke OpenClipboard, NULL<br />    invoke GetClipboardData, CF_TEXT<br />    push esi<br />    push edi<br />    mov esi, eax<br /><br />    invoke StrLen, esi<br />    mov edi, eax<br /><br />@@&#58; xor eax, eax<br />    mov al, BYTE PTR &#91;esi&#93;<br />    invoke SendMessage, hEdit, WM_CHAR, eax, 0<br />    inc esi<br />    dec edi<br />    jnz @B<br /><br />    pop edi<br />    pop esi<br />    invoke CloseClipboard<br /></code></pre><br /><br />It is better in the sense that it doesn't delete the old data, then reprocess it. With the other algo, the data in edit box was re-processed every time a paste occured. By rights any code in the box needs to be in the correct format, otherwise it couldn't have got in there, so why check it again when you paste?<br /><br />Unfortunately this code makes no attempt to open the clipboard properly (ie if it fails it carries on reguardless, which is bad).<br /><br />Mirno</div>
    <div class="meta">Posted on 2001-08-16 09:38:00 by Mirno</div>
   </div>
   <div class="post" id="post-3898">
    <div class="subject"><a href="#post-3898">Edit Control Subclassing,what am i doing wrong</a></div>
    <div class="body">Yuk, this1 works for me fine.. <pre><code><br /><br />EditProc  	PROC 		;<br />; Process control messages 	;<br />	mov	eax, &#91;esp+8&#93;	; message<br />;........????...................;<br />	cmp	eax,WM_PASTE	; Msg<br />	jne	EditProc_4	;<br />;.WM_PASTE .......????..........;<br />	push	CF_TEXT		;<br />	call	IsClipboardFormatAvailable <br />	test	eax, eax	;<br />	jz	EditProc_Ret_0	;<br />	push	hWnd		;<br />	call	OpenClipboard	;<br />	test	eax, eax	;<br />	jz	EditProc_Ret_0	;<br />	push	CF_TEXT		;<br />	call	GetClipboardData;<br />	test	eax, eax	;<br />	jz	EditProc_Ret_0	;<br />	push	eax		; for GlobalUnlock<br />	push	eax		;<br />	call	GlobalLock	;<br />	test	eax, eax	;<br />	jnz	EditProc_1	;	<br />	pop	eax		;<br />	jz	EditProc_Ret_0	;<br />EditProc_1&#58;			;<br />	lea	edi, Buffer1	;<br />	mov	ebx, 5 		;<br />	xor	ecx, ecx	;<br />	xor	edx, edx	;<br />EditProc_2&#58;			;<br />	mov	cl, &#91;eax&#93;	;<br />	inc	eax		;<br />	test	ecx, ecx	;<br />	jz	EditProc_3	;<br />	cmp	cl, 30h		; dl = 30h<br />	jl	EditProc_2	;<br />	cmp	cl, 39h		; dh = 39h<br />	jg	EditProc_2	;<br />	mov	&#91;edi+edx&#93;, cl	;<br />	inc	edx		;	<br />	cmp	edx, ebx	; ebx=5<br />	jl	EditProc_2	;<br />EditProc_3&#58;			;<br />	mov	&#91;edi+edx&#93;, ch	; ch=0<br />	call	GlobalUnlock	;<br />	inc	ebx		;<br />	push	offset Buffer1	; &#91;esp+16&#93; lParam<br />	push	ebx		; &#91;esp+12&#93; wParam<br />	call	CloseClipboard	;<br />				;<br />	mov	eax, hEdit	;<br />	push	WM_SETTEXT	; &#91;esp+8&#93; message<br />	mov	ecx, lpfnEdProc	;<br />	push	eax		;<br />	push	ecx		;<br />	call	CallWindowProc	;	<br />				;<br />	push	0		; &#91;esp+16&#93; lParam<br />	push	VK_RETURN	; &#91;esp+12&#93; wParam<br />	mov	eax, hEdit	; <br />	push	WM_CHAR		; &#91;esp+8&#93; message<br />	mov	ecx, lpfnEdProc	;<br />	push	eax		;<br />	push	ecx		;<br />	call	CallWindowProc	;<br />				;<br />	mov	eax, 10		; &#91;esp+16&#93; lParam<br />	mov	edx, EM_SETSEL	; &#91;esp+8&#93; message<br />	mov	&#91;esp+16&#93;,eax	; &#91;esp+16&#93; lParam<br />	mov	&#91;esp+12&#93;, eax	; &#91;esp+12&#93; wParam<br />	mov	&#91;esp+8&#93;, edx	; &#91;esp+8&#93; message<br />	jmp	EditProc_Call	;<br />EditProc_4&#58;			;<br />	cmp	eax, WM_CHAR	;<br />	jnz	EditProc_Call	;<br />;.. WM_CHAR ....????............;<br />	mov	eax, &#91;esp+12&#93; 	; wParam<br />	cmp	eax, 30h	;<br />	jl	EditProc_Call	;<br />	cmp	eax, 39h	;<br />	jg	EditProc_Call 	;<br />	push	0		; &#91;esp+16&#93; lParam<br />	mov	eax, hEdit	;<br /> 	push	0		; &#91;esp+12&#93; wParam<br />	push	WM_GETTEXTLENGTH; &#91;esp+8&#93; message<br />	mov	ecx, lpfnEdProc	;<br />	push	eax		;<br />	push	ecx		;<br />	call	CallWindowProc	;	<br />	cmp	eax, 4		; 5-1 No more than 5 numbers<br />	jle	EditProc_Call	;<br />EditProc_Ret_0&#58;			;		<br />	xor	eax, eax	;<br />	jz	EditProc_Ret	;<br />EditProc_Call&#58;			;<br />	mov	eax, &#91;esp+16&#93;	;<br />	mov	ecx, &#91;esp+12&#93;	;<br />	mov	edx, &#91;esp+8&#93;	;<br />	push	eax		;<br />	push	ecx		;<br />	mov	eax, hEdit	;<br />	push	edx		;<br />	mov	ecx, lpfnEdProc	;<br />	push	eax		;<br />	push	ecx		;<br />	call	CallWindowProc	;<br />	mov	eax, 1		;<br />EditProc_Ret&#58;			;<br />	ret	16		; ret 16<br />EditProc	ENDP		;</code></pre></div>
    <div class="meta">Posted on 2001-08-16 10:07:13 by buliaNaza</div>
   </div>
   <div class="post" id="post-5032">
    <div class="subject"><a href="#post-5032">Edit Control Subclassing,what am i doing wrong</a></div>
    <div class="body"><div class="quote">You can also do this without the buffer (or the tlen because I used esi &amp; edi instead). </div> <br /><br />If translate in Russian term Clipboard and then litteraly translate in English it would be &quot;exchange buffer&quot; :)<br />So I meant Clipboard by &quot;buffer&quot; :)<br />I think bulyNaza understood what I meant since she knows Russian.<br /><br />A few more things about the task:<br />Before implementing anything about  WM_PASTE we need to know what exactly we want form the control to do with data in Clipboard(buffer).<br />There may be different scenarios.<br />After we check it for valid simbols and found invalid ones we can<br />do one of possible things<br />1. If there are invalid simbols - cancel the operation<br />2. If there are invalid simbols - filter them out.<br /> <br />In any case to do the pasting I would use EM_REPLACESEL<br />to perform final pasting.<br />In first approach <br />I search for invalid simbols in Clipboard and if there is none<br />I send EM_REPLACESEL with address of Clipboard data.<br />Else - exit doing nothing.<br />In the second approach I would formate a string filtering out invalid simbols from buffer (or doing it exactly in very Clipboard memory) and then send EM_REPLACESEL if first byte of formated string is not 0 (it would happens if there where no valid characters at all)</div>
    <div class="meta">Posted on 2001-08-27 06:43:25 by The Svin</div>
   </div>
   <div class="post" id="post-5042">
    <div class="subject"><a href="#post-5042">thankyou Svin</a></div>
    <div class="body">Yes I meant a second approach. To filter out unwanted symbols,and paste those which left. But the problem was like this:<br />let we have this text in clipboard :<br />&quot;00 11 22 33 44 55 66 FF &quot; two hex digit sequences delimited by spaces. And we should Get : 00112233445566FF after paste.<br />But thelength of an EDIT is limited to 16 symbols.<br />So ... hereis the result :(</div>
    <div class="meta">Posted on 2001-08-27 08:52:09 by Yuk</div>
   </div>
  </div>
 </body>
</html>