<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>help,KetilO! - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=20230" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=42">RadASM</a> &raquo; <a href="../?id=20230">help,KetilO!</a></p>
   <div class="post" id="post-154932">
    <div class="subject"><a href="#post-154932">help,KetilO!</a></div>
    <div class="body">I want to set a static control into a HyperLink, just like About of RadAsm. But the string in that static control disappear when it run.  Here is the code sample:<br /><br /><br />LinkProc    proc hWin:HWND,uMsg:UINT,wParam:WPARAM,lParam:LPARAM<br />    pushad<br />    mov    eax,uMsg<br />    .if	eax==WM_LBUTTONUP<br />        invoke    ShellExecute,0,0,CTXT(&quot;http://www.vxer.net&quot;),0,0,0<br />    .elseif eax==WM_SETCURSOR<br />        invoke    SetCursor,hCur   ;the hand cursor<br />    .else<br />        invoke    GetWindowLong,hWin,GWL_USERDATA<br />        mov        ebx,eax<br />        invoke    CallWindowProc,ebx,hWin,uMsg,wParam,lParam<br />    .endif<br />    popad<br />    mov	eax,TRUE<br />    ret<br />LinkProc endp<br /><br />SetLinkProc    proc<br />LOCAL      _hWnd<br />    pushad<br />    invoke	GetDlgItem,hDlg,IDC_STC_LINK  <br />    mov	_hWnd,eax<br />    invoke	SetWindowLong,_hWnd,GWL_WNDPROC,addr LinkProc<br />    invoke	SetWindowLong,_hWnd,GWL_USERDATA,eax<br />    popad<br />    ret<br />SetLinkProc endp<br /><br /><br />What's wrong?</div>
    <div class="meta">Posted on 2004-12-28 22:49:53 by dyna</div>
   </div>
   <div class="post" id="post-154936">
    <div class="subject"><a href="#post-154936">help,KetilO!</a></div>
    <div class="body">Look at the MasmEd source.<br /><br /><br /><br /><br />/siddhartha</div>
    <div class="meta">Posted on 2004-12-29 00:41:34 by siddhartha</div>
   </div>
   <div class="post" id="post-154942">
    <div class="subject"><a href="#post-154942">i'v found the problem, but still don't know how to solve it</a></div>
    <div class="body">When I add a manifest resource into the project, the string will disappear.<br />And if I delete the manifest, it's OK.<br />So I think the problem comes from the manifest resource.<br /><br />That manifest is a XML file, to give software a XP-Style interface.<br />Here is the XP.xml<br /><br />&lt;?xml version=&quot;1.0&quot; encoding=&quot;UTF-8&quot; standalone=&quot;yes&quot;?&gt;<br />&lt;assembly xmlns=&quot;urn:schemas-microsoft-com:asm.v1&quot; manifestVersion=&quot;1.0&quot;&gt;<br />&lt;assemblyIdentity<br />    name=&quot;Microsoft.Windows.Shell.ClassXP&quot;<br />    processorArchitecture=&quot;x86&quot;<br />    version=&quot;1.0.0.0&quot;<br />    type=&quot;win32&quot;/&gt;<br />&lt;description&gt;ClassXP&lt;/description&gt;<br />&lt;dependency&gt;<br />    &lt;dependentAssembly&gt;<br />        &lt;assemblyIdentity<br />            type=&quot;win32&quot;<br />            name=&quot;Microsoft.Windows.Common-Controls&quot;<br />            version=&quot;6.0.0.0&quot;<br />            processorArchitecture=&quot;x86&quot;<br />            publicKeyToken=&quot;6595b64144ccf1df&quot;<br />            language=&quot;*&quot;<br />        /&gt;<br />    &lt;/dependentAssembly&gt;<br />&lt;/dependency&gt;<br />&lt;/assembly&gt;<br /><br /><br />Who knows how to solve the problem?</div>
    <div class="meta">Posted on 2004-12-29 02:09:59 by dyna</div>
   </div>
   <div class="post" id="post-154951">
    <div class="subject"><a href="#post-154951">help,KetilO!</a></div>
    <div class="body">Hi dyna<br /><br />Nothing wrong with the manifest, a few bugs with your subclass proc.<br /><br /><pre><code><br />.386<br />.model flat, stdcall  ;32 bit memory model<br />option casemap &#58;none  ;case sensitive<br /><br />include UrlControl.inc<br /><br />.code<br /><br />start&#58;<br /><br />	invoke GetModuleHandle,NULL<br />	mov		hInstance,eax<br /><br />    invoke InitCommonControls<br />	invoke DialogBoxParam,hInstance,IDD_DIALOG1,NULL,addr DlgProc,NULL<br />	invoke ExitProcess,0<br /><br />;########################################################################<br /><br />LinkProc proc hWin&#58;HWND,uMsg&#58;UINT,wParam&#58;WPARAM,lParam&#58;LPARAM <br />	LOCAL	buffer&#91;256&#93;&#58;BYTE<br /><br />	mov eax,uMsg <br />	.if eax==WM_LBUTTONUP <br />		invoke GetWindowText,hWin,addr buffer,sizeof buffer<br />		invoke ShellExecute,0,0,addr buffer,0,0,0 <br />		xor eax,eax<br />	.elseif eax==WM_SETCURSOR <br />		invoke LoadCursor,0,IDC_HAND<br />		invoke SetCursor,eax ;the hand cursor <br />		xor eax,eax<br />	.else <br />		invoke GetWindowLong,hWin,GWL_USERDATA <br />		invoke CallWindowProc,eax,hWin,uMsg,wParam,lParam <br />	.endif <br />	ret <br /><br />LinkProc endp <br /><br />SetLinkProc proc hDlg&#58;HWND<br />	LOCAL _hWnd<br /><br />	invoke GetDlgItem,hDlg,1001<br />	mov _hWnd,eax <br />	invoke SetWindowLong,_hWnd,GWL_WNDPROC,addr LinkProc <br />	invoke SetWindowLong,_hWnd,GWL_USERDATA,eax <br />	ret <br /><br />SetLinkProc endp <br /><br />DlgProc proc hWin&#58;HWND,uMsg&#58;UINT,wParam&#58;WPARAM,lParam&#58;LPARAM<br /><br />	mov		eax,uMsg<br />	.if eax==WM_INITDIALOG<br />		invoke SetLinkProc,hWin<br />	.elseif eax==WM_COMMAND<br />		mov		edx,wParam<br />		movzx	eax,dx<br />		shr		edx,16<br />		.if edx==BN_CLICKED<br />			.if eax==IDCANCEL<br />				invoke SendMessage,hWin,WM_CLOSE,NULL,NULL<br />			.endif<br />		.endif<br />	.elseif eax==WM_CLOSE<br />		invoke EndDialog,hWin,0<br />	.else<br />		mov		eax,FALSE<br />		ret<br />	.endif<br />	mov		eax,TRUE<br />	ret<br /><br />DlgProc endp<br /><br />end start<br /></code></pre><br /><br />KetilO</div>
    <div class="meta">Posted on 2004-12-29 03:42:27 by KetilO</div>
   </div>
   <div class="post" id="post-154952">
    <div class="subject"><a href="#post-154952">haha</a></div>
    <div class="body">it works.<br /><br />That means I should LoadCursor and SetCursor in the Contol's DlgProc, right? But why should return FALSE? (xor eax,eax) I tried set eax ==1, it's also OK.<br /><br /><br /><br /><br />thank u very much, Ketilo, every time I ask for help, u always helped me!</div>
    <div class="meta">Posted on 2004-12-29 04:19:14 by dyna</div>
   </div>
   <div class="post" id="post-154955">
    <div class="subject"><a href="#post-154955">help,KetilO!</a></div>
    <div class="body">another problem, or maybe an advice.<br />it seem's that if some registers value be changed in WndProc, it will crash on Windows2000.<br /><br />For example:<br />i use New Project--&gt;Win32 App--&gt;DialogApp.tpl to build a exe file. It runs OK both in WinXP and Win2000.<br /><br />Then I add some code like this:<br /><br /><pre><code>.if eax==WM_INITDIALOG<br />   mov	ebx,123123<br />   mov	ecx,123123</code></pre><br /><br />Then the EXE file will crash on Win2000, but Ok on WinXP.<br />So I always change the template code by inject PUSHAD and POPAD.<br /><br /><pre><code><br />DlgProc proc hWin&#58;HWND,uMsg&#58;UINT,wParam&#58;WPARAM,lParam&#58;LPARAM<br />    pushad    <br />    mov		eax,uMsg<br />    .if eax==WM_INITDIALOG<br />        mov	ebx,123123<br />        mov	ecx,123123<br />    .elseif eax==WM_COMMAND<br />    <br />    .elseif eax==WM_CLOSE<br />        invoke EndDialog,hWin,0<br />    .else<br />        popad<br />        mov		eax,FALSE<br />        ret<br />    .endif<br />    popad<br />    mov		eax,TRUE<br />    ret<br />DlgProc endp</code></pre><br /><br />So i think u could change the template code for more compatibility.<br />I test this on Simplified Chinese Windows2000 and Windows XP.<br />Or maybe it's just my fool mistake.  haha</div>
    <div class="meta">Posted on 2004-12-29 04:53:40 by dyna</div>
   </div>
   <div class="post" id="post-154956">
    <div class="subject"><a href="#post-154956">help,KetilO!</a></div>
    <div class="body">Hi dyna<br /><br />Be careful what registers you use in a windows callback function. ebx,esi and edi must be preserved. Also the return value from a callback is important. The LoadCursor function has nothing to do with it.<br /><br />Preserving registers in a callback:<br /><br /><pre><code><br />DlgProc proc uses ebx esi edi,hWin&#58;HWND,uMsg&#58;UINT,wParam&#58;WPARAM,lParam&#58;LPARAM <br /></code></pre><br /><br />KetilO</div>
    <div class="meta">Posted on 2004-12-29 05:31:38 by KetilO</div>
   </div>
   <div class="post" id="post-154975">
    <div class="subject"><a href="#post-154975">thx</a></div>
    <div class="body">thank u for the answer.<br />Because I'm not good at GDI programming, I seldom wrote programs with interface. :cry: <br /><br />The last question(hope not bother u :oops: )<br /><br />How can I change the color of the text in a static control,when mouse moves in and change the color back when mouse move elsewhere?<br />Just like RadASM About, it becomes blue with underline.<br /><br />I'v read some source code, but they all use DrawText directly, not a static control.<br /><br />can u help me? or give me some sample code?<br /><br />best wishes for u!</div>
    <div class="meta">Posted on 2004-12-29 09:13:40 by dyna</div>
   </div>
   <div class="post" id="post-154988">
    <div class="subject"><a href="#post-154988">help,KetilO!</a></div>
    <div class="body">Hi dyna<br /><br /><pre><code><br />IDD_DLGABOUT	equ 4900<br />IDC_EDTABOUT	equ 1001<br />IDC_URL			equ 1002<br /><br />.data?<br /><br />OldUrlProc		dd ?<br />fMouseOver		dd ?<br />hUrlFont		dd ?<br />hUrlFontU		dd ?<br />hUrlBrush		dd ?<br /><br />.code<br /><br />UrlProc proc hWin&#58;HWND,uMsg&#58;UINT,wParam&#58;WPARAM,lParam&#58;LPARAM<br />	LOCAL	rect&#58;RECT<br />	LOCAL	buffer&#91;128&#93;&#58;BYTE<br /><br />	mov		eax,uMsg<br />	.if eax==WM_MOUSEMOVE<br />		invoke GetClientRect,hWin,addr rect<br />		invoke GetCapture<br />		.if eax!=hWin<br />			mov		fMouseOver,TRUE<br />			invoke SetCapture,hWin<br />			invoke SendMessage,hWin,WM_SETFONT,hUrlFontU,TRUE<br />		.endif<br />		mov		edx,lParam<br />		movzx	eax,dx<br />		shr		edx,16<br />		.if eax&gt;rect.right || edx&gt;rect.bottom<br />			mov		fMouseOver,FALSE<br />			invoke ReleaseCapture<br />			invoke SendMessage,hWin,WM_SETFONT,hUrlFont,TRUE<br />		.endif<br />	.elseif eax==WM_LBUTTONUP<br />		mov		fMouseOver,FALSE<br />		invoke ReleaseCapture<br />		invoke SendMessage,hWin,WM_SETFONT,hUrlFont,TRUE<br />		invoke GetWindowText,hWin,addr buffer,sizeof buffer<br />		invoke ShellExecute,hWnd,addr iniOpen,addr buffer,NULL,NULL,SW_SHOWNORMAL<br />	.elseif eax==WM_SETCURSOR<br />		invoke LoadCursor,NULL,IDC_HAND<br />		invoke SetCursor,eax<br />	.else<br />		invoke CallWindowProc,OldUrlProc,hWin,uMsg,wParam,lParam<br />		ret<br />	.endif<br />	xor		eax,eax<br />	ret<br /><br />UrlProc endp<br /><br />AboutProc proc hWin&#58;HWND,uMsg&#58;UINT,wParam&#58;WPARAM,lParam&#58;LPARAM<br />	LOCAL	lf&#58;LOGFONT<br /><br />	mov		eax,uMsg<br />	.if eax==WM_INITDIALOG<br />		invoke SetWindowText,hWin,addr AppName<br />		invoke SendDlgItemMessage,hWin,IDC_EDTABOUT,WM_SETTEXT,0,addr AboutMsg<br />		invoke SendDlgItemMessage,hWin,IDC_URL,WM_SETTEXT,0,addr AboutUrl<br />		invoke GetDlgItem,hWin,IDC_URL<br />		invoke SetWindowLong,eax,GWL_WNDPROC,addr UrlProc<br />		mov		OldUrlProc,eax<br />		invoke SendMessage,hWin,WM_GETFONT,0,0<br />		mov		hUrlFont,eax<br />		invoke GetObject,hUrlFont,sizeof LOGFONT,addr lf<br />		mov		lf.lfUnderline, TRUE<br />		invoke CreateFontIndirect,addr lf<br />		mov		hUrlFontU,eax<br />		invoke GetSysColor,COLOR_3DFACE<br />		invoke CreateSolidBrush,eax<br />		mov		hUrlBrush,eax<br />	.elseif eax==WM_COMMAND<br />		mov		eax,wParam<br />		mov		edx,eax<br />		shr		edx,16<br />		and		eax,0FFFFh<br />		.if edx==BN_CLICKED<br />			.if eax==IDOK<br />				invoke SendMessage,hWin,WM_CLOSE,NULL,NULL<br />			.endif<br />		.endif<br />	.elseif eax==WM_CTLCOLORSTATIC<br />		invoke GetDlgItem,hWin,IDC_URL<br />		mov		edx,eax<br />		xor		eax,eax<br />		.if edx==lParam<br />			.if fMouseOver<br />				mov		eax,0FF0000h<br />			.endif<br />			invoke SetTextColor,wParam,eax<br />			invoke SetBkMode,wParam,TRANSPARENT<br />			mov		eax,hUrlBrush<br />		.endif<br />		ret<br />	.elseif eax==WM_CLOSE<br />		invoke DeleteObject,hUrlFontU<br />		invoke DeleteObject,hUrlBrush<br />		invoke EndDialog,hWin,NULL<br />	.else<br />		mov eax,FALSE<br />		ret<br />	.endif<br />	mov  eax,TRUE<br />	ret<br /><br />AboutProc endp<br /></code></pre><br />This is things you can download from my website.<br /><br />KetilO</div>
    <div class="meta">Posted on 2004-12-29 14:22:00 by KetilO</div>
   </div>
   <div class="post" id="post-155001">
    <div class="subject"><a href="#post-155001">re</a></div>
    <div class="body">thank you KetilO, i succeed.<br />Hope it's not too late to say: Merry Christmas and Happy New Year!</div>
    <div class="meta">Posted on 2004-12-29 21:52:49 by dyna</div>
   </div>
  </div>
 </body>
</html>