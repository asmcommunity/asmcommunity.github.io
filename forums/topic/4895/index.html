<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>MP3 information - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=4895" />
    <link rel="next" href="../?id=4895&amp;page=2" /> </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=5">Algorithms &amp; Source Code</a> &raquo; <a href="../?id=4895">MP3 information</a></p>
<form class="pagination" action="../" method="get"><a href="../?id=4895&amp;page=1" style="visibility:hidden;">&laquo;</a><a href="../?id=4895&amp;page=0" style="visibility:hidden;">&lt;</a><input type="hidden" name="id" value="4895" /><input type="number" name="page" min="1" max="2" step="1" value="1" onchange="this.form.submit();" /><a href="../?id=4895&amp;page=2">&gt;</a><a href="../?id=4895&amp;page=2">&raquo;</a></form>   <div class="post" id="post-34314">
    <div class="subject"><a href="#post-34314">MP3 information</a></div>
    <div class="body">This is a little proc I've coded to get information from an MP3 file. As input it requires a filename and a pointer to an MP3INFO structure that will receive the information (if the proc returns true).<br /><pre><code><br />MP3INFO STRUCT<br />    fileSize        dword   ?   ; the total filesize, in bytes<br />    fileLength      dword   ?   ; length of MP3, in msec<br />    bitrate         sdword  ? <br />;note&#58; the bitrate is in bits per second. if bitrate is negative,<br />;the MP3 uses a variable bitrate. In this case, the value is<br />;the negated average bitrate.<br />    frequency       dword   ?   ; in Hertz<br />    nFrames         dword   ?   ; number of MPEG frames<br />MP3INFO ENDS<br /></code></pre><br /><br />The number of frames seem to be different from what I get in winamp, however I think winamp does not actually count them, but calculates them by taking one frame size, and dividing the filesize by that frame size. This value exactly matches the value winamp shows, but isn't correct (I think) as it does not account frame padding. For VBR mp3's however, winamp does count them (it has to), and the values match.<br /><br />The code is optimized for size (442 bytes now), not for speed, as it would be pretty useless due to the file operations.<br /><br />Feel free to optimize it further (in size) :)<br /><br /><pre><code><br />.data<br />    bitrateTable    dw -1,32,40,48,56,64,80,96,112,128,160,192,224,256,320,-1<br />    samplerateTable dw 44100,48000,32000<br />.code<br /><br />; #########################################################################<br /><br /><br />GetMP3Info  proc uses esi edi ebx lpFilename&#58;DWORD, lpInfoStruct&#58;DWORD<br />LOCAL   hFile&#58;DWORD<br />LOCAL   hMap&#58;DWORD<br />LOCAL   pMap&#58;DWORD<br />LOCAL   totalbitrate&#58;DWORD<br />LOCAL   totalaudiosize&#58;DWORD<br />    xor     ebx, ebx<br />    mov     hFile, ebx<br />    mov     hMap, ebx<br />    mov     pMap, ebx<br />    mov     edi, lpInfoStruct<br />    push    edi<br />    or      eax, -1<br />    mov     ecx, &#40;sizeof MP3INFO&#41; / 4<br />    rep     stosd<br />    mov     totalbitrate, ebx<br />    mov     totalaudiosize,ebx<br />    <br />    assume  edi&#58;PTR MP3INFO<br />    pop     edi<br />    mov     &#91;edi&#93;.nFrames, ebx<br /><br />    invoke  CreateFile, lpFilename, GENERIC_READ, FILE_SHARE_READ,\<br />                ebx, OPEN_EXISTING, ebx, ebx<br />    cmp     eax, INVALID_HANDLE_VALUE<br />    je      _invalid<br />    <br />    mov      hFile, eax<br />    invoke   CreateFileMapping, eax, ebx, PAGE_READONLY, ebx,ebx,ebx<br />    or      eax, eax<br />    jz      _invalid<br />    <br />    mov      hMap, eax<br />    <br />    invoke  MapViewOfFile, eax, FILE_MAP_READ,ebx,ebx,ebx<br />    or      eax, eax<br />    jz      _invalid<br />    <br />    mov     pMap, eax<br />    mov     esi, eax<br /><br />    invoke  GetFileSize, hFile, NULL<br />    mov     &#91;edi&#93;.fileSize, eax<br />    lea     ebx, &#91;eax+esi&#93;<br /><br />    _gotonextframe&#58;<br />    cmp     byte ptr &#91;esi&#93;, 0FFh<br />    je      @F<br />    inc     esi<br />    cmp     esi, ebx<br />    jb      _gotonextframe<br />    jmp     _done<br />    @@&#58;<br />    cmp     esi, ebx<br />    je      _done<br />    inc     esi<br />    mov     al, &#91;esi&#93;<br />    test    al, 11100000b<br />    jz      _gotonextframe<br />    inc     esi<br /><br />    _frame_found&#58;<br />    <br />    inc     &#91;edi&#93;.nFrames<br />    mov     cl, al<br />    and     cl, 00011110b<br />    cmp     cl, 00011010b   ;MPEG v1, layer 3?<br />    jne     _not_supported<br />    mov     eax, ebx<br />    sub     eax, esi<br />    cmp     eax, 2 ;at least 2 bytes readable?<br />    jb      _invalid<br />    movzx   eax, byte ptr &#91;esi&#93;<br />    mov     ecx, eax<br />    sub     esi, 2 ;point back to start of frame, as framesize is added later<br />    shr     al, 4<br />    movsx   eax, word ptr &#91;bitrateTable&#93;&#91;2*eax&#93;<br /><br />    cmp     eax, -1<br />    je      _invalid<br />    ; eax is bitrate<br />    add     totalbitrate, eax<br />    mov     edx, &#91;edi&#93;.bitrate<br />    cmp     edx, -1<br />    jne     _bitrate_already_set<br />    mov     &#91;edi&#93;.bitrate, eax<br />    jmp     _fixed_bitrate_so_far<br />    _bitrate_already_set&#58;<br />    cmp     eax, edx        ;current bitrate same as set bitrate?<br />    je      _fixed_bitrate_so_far<br />    ;variable bitrate<br />    and     &#91;edi&#93;.bitrate, 0    ;set to zero indicating variable bitrate<br />    ;fall through...<br />    _fixed_bitrate_so_far&#58;<br /><br />    xor     edx, edx ; init framesize to zero<br />    shr     ecx, 2<br />    setc    dl       ;last bit shifted out is padding bit,<br />                     ; if set increase framesize by one<br /><br />    and     ecx, 3<br />    cmp     ecx, 3<br />    je      _invalid<br />    movzx   ecx, word ptr &#91;samplerateTable&#93;&#91;2*ecx&#93;<br />    mov     &#91;edi&#93;.frequency, ecx<br /><br />    ;FrameSize = 144 * BitRate / SampleRate + Padding<br />    ; eax = bitrate&#40;in kbps, needs to be converted to bps yet&#41;<br />    ; ecx = sampleRate<br />    ; edx = padding<br />    ; framesize = 144 * eax / ecx + edx<br />    <br />    push    edx<br />    imul    eax, 144*1000<br />    xor     edx, edx<br />    div     ecx<br />    pop     edx<br />    add     edx, eax<br />    <br />    ; edx is framesize now<br /><br />    add     esi, edx<br />    cmp     esi, ebx<br />    ja      _invalid    ; frame bigger than file, invalid<br />    je      _done<br /><br />    add     totalaudiosize, edx<br /><br />    jmp     _gotonextframe<br /><br />    _not_supported&#58;<br />    _invalid&#58;<br />    xor     eax, eax<br />    jmp     _exit<br /><br />    _done&#58;<br />    mov ebx, 1000<br />    <br />    mov     eax, &#91;edi&#93;.bitrate  ;get bitrate in kbps<br />    mul     ebx                 ;*1000, in bpsd<br />    mov     &#91;edi&#93;.bitrate, eax  ;set new bitrate, in bps<br />    mov     ecx, eax            ;set ecx to bitrate<br />    or      eax, eax            ;bitrate zero?<br />    jnz     _fixed_bitrate      ;if not, fixed<br />    mov     eax, totalbitrate   ;else, variable. get total &#40;sum of all bitrates&#41;<br />    mul     ebx                 ;multiple by 1000<br />    mov     ecx, &#91;edi&#93;.nFrames  ;get number of frames<br />    or      ecx, ecx            ;no frames?<br />    jz      _invalid            ;bye<br />    div     ecx                 ;divide by number of frames<br />    mov     ecx, eax            ;set ecx to positive bitrate<br />    neg     eax                 ;negate, indicating a variable bitrate<br />    mov     &#91;edi&#93;.bitrate, eax  <br />    _fixed_bitrate&#58;<br />    ; eax is bitrate here, either fixed or averaged value<br />    shl     ebx, 3  ;1000*8=8000<br />    <br />    mov     eax, totalaudiosize<br />    mul     ebx ; * 8000, convert bytes to bits and seconds to miliseconds<br />    div     ecx ; divide by positive bitrate in bps<br />    mov     &#91;edi&#93;.fileLength, eax   ;set filelengths, in msecs<br />    <br />    ;calculate time<br />    ;bitrate <br />    assume  edi&#58;nothing<br />    xor     eax, eax<br />    inc     eax<br />    <br />    _exit&#58;<br />    push    eax<br />    <br />    mov     eax, pMap<br />    or      eax, eax<br />    jz      @F<br />    invoke  UnmapViewOfFile, eax<br />    @@&#58;<br />    <br />    mov     eax, hMap<br />    or      eax, eax<br />    jz      @F<br />    invoke  CloseHandle, eax<br />    @@&#58;<br />    <br />    mov     eax, hFile<br />    or      eax, eax<br />    jz      @F<br />    invoke  CloseHandle, eax<br />    @@&#58;<br />    <br />    pop     eax<br />ret<br />GetMP3Info endp<br /></code></pre><br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-21 14:53:52 by Thomas</div>
   </div>
   <div class="post" id="post-34334">
    <div class="subject"><a href="#post-34334">MP3 information</a></div>
    <div class="body">Oh! size optimization is always pleasure :)<br />No testing needed, not procs difference talks :)<br />Are FPU or MMX opcodes allowed?</div>
    <div class="meta">Posted on 2002-04-21 16:03:30 by The Svin</div>
   </div>
   <div class="post" id="post-34338">
    <div class="subject"><a href="#post-34338">MP3 information</a></div>
    <div class="body">Yes that's okay but the proc may change a lot, I'm still working on it. It has several bugs and does not support mpeg 2 &amp; 2.5 yet.<br />I'll post the new version later, need some sleep first :)<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-21 16:16:00 by Thomas</div>
   </div>
   <div class="post" id="post-34474">
    <div class="subject"><a href="#post-34474">MP3 information</a></div>
    <div class="body">New and much more stable version:<br /><pre><code><br />.data<br />    ;V1 layer 3 bitrates<br />    bitrateTableV1  dw -1,32,40,48,56,64,80,96,112,128,160,192,224,256,320,-1<br />    ;V2/2.5 layer 3 bitrates<br />    bitrateTableV2  dw -1,8,16,24,32,40,48,56,64,80,96,112,128,144,160,-1<br /><br /><br />    samplerateTableV1   dw 44100,48000,32000<br />    samplerateTableV2   dw 22050,24000,16000<br />    samplerateTableV2_5 dw 11025,12000,8000<br /><br />.code<br /><br />; #########################################################################<br />MP3INFO STRUCT<br />    fileSize        dword   ?   ; in bytes<br />    fileLength      dword   ?   ; in miliseconds<br />    bitrate         sdword  ?   ; in bps. if negative&#58; variable bitrate,bitrate=negated average<br />    frequency       dword   ?   ; in Hertz<br />    nFrames         dword   ?   ; number of MPEG frames<br />MP3INFO ENDS<br /><br /><br />GetMP3Info  proc uses esi edi ebx lpFilename&#58;DWORD, lpInfoStruct&#58;DWORD<br />LOCAL   hFile&#58;DWORD<br />LOCAL   hMap&#58;DWORD<br />LOCAL   pMap&#58;DWORD<br />LOCAL   totalbitrate&#58;DWORD<br />LOCAL   srTablePtr&#58;DWORD<br />    xor     ebx, ebx<br />    mov     hFile, ebx<br />    mov     hMap, ebx<br />    mov     pMap, ebx<br />    mov     edi, lpInfoStruct<br />    push    edi<br />    or      eax, -1<br />    mov     ecx, &#40;sizeof MP3INFO&#41; / 4<br />    rep     stosd<br />    mov     totalbitrate, ebx<br /><br />    assume  edi&#58;PTR MP3INFO<br />    pop     edi<br />    mov     &#91;edi&#93;.nFrames, ebx<br /><br />    invoke  CreateFile, lpFilename, GENERIC_READ, FILE_SHARE_READ,\<br />                ebx, OPEN_EXISTING, ebx, ebx<br />    cmp     eax, INVALID_HANDLE_VALUE<br />    je      _invalid<br /><br />    mov      hFile, eax<br />    invoke   CreateFileMapping, eax, ebx, PAGE_READONLY, ebx,ebx,ebx<br />    or      eax, eax<br />    jz      _invalid<br /><br />    mov      hMap, eax<br /><br />    invoke  MapViewOfFile, eax, FILE_MAP_READ,ebx,ebx,ebx<br />    or      eax, eax<br />    jz      _invalid<br /><br />    mov     pMap, eax<br />    mov     esi, eax<br /><br />    invoke  GetFileSize, hFile, NULL<br />    mov     &#91;edi&#93;.fileSize, eax<br />    lea     ebx, &#91;eax+esi&#93;<br /><br />    _gotonextframe&#58;<br />    ; find first 8 bits of sync bits<br />    cmp     byte ptr &#91;esi&#93;, 0FFh<br />    je      @F<br />    inc     esi<br />    cmp     esi, ebx<br />    jb      _gotonextframe<br />    jmp     _done<br /><br />    ;possible first 8 sync bits found<br />    @@&#58;<br />    DPrint  &quot;8 sync bits found&quot;<br />    inc     esi<br />    cmp     esi, ebx<br />    jae     _done<br />    mov     al, &#91;esi&#93;<br />    mov     cl, al<br />    and     cl, 11100000b<br />    IFDEF _DEBUG<br />        .IF cl==11100000b<br />            DPrint &quot;11 sync bits found in total!&quot;<br />        .ELSE<br />            DPrint &quot;no more 1 bits found, no sync&quot;<br />        .ENDIF<br />    ENDIF<br />    cmp     cl, 11100000b<br />    jne     _gotonextframe<br />    inc     esi<br /><br />    _frame_found&#58;<br />    DPrint  &quot;frame found&quot;<br />    mov     cl, al<br />    and     cl, 00011110b   ; 000BBCC0, BB=MPEG audio version ID, CC=layer description<br />    <br />    mov     edx, offset bitrateTableV1<br />    mov     srTablePtr, offset samplerateTableV1<br />    cmp     cl, 00011010b   ;MPEG v1, layer 3?<br />    je      _frame_continue<br />        <br />    mov     edx, offset bitrateTableV2<br />    mov     srTablePtr,  offset samplerateTableV2<br />    cmp     cl, 00010010b ;MPEG v2, layer 3?<br />    je      _frame_continue<br />    <br />    mov     srTablePtr, offset samplerateTableV2_5<br />    IFDEF _DEBUG<br />        .IF cl!=00000010b<br />            DPrint &quot;unsupported format 1&quot;<br />        .ENDIF<br />    ENDIF<br />    cmp     cl, 00000010b ;MPEG v2.5, layer 3<br />    jne     _gotonextframe  ;find next frame<br />    PrintText &quot;falling&quot;<br />    ;fall through...<br />    _frame_continue&#58;<br />    PrintText &quot;---frame continue---&quot;<br />    mov     eax, ebx<br />    sub     eax, esi<br /><br />    cmp     eax, 2 ;at least 2 bytes readable?<br />    jb      _invalid<br />    movzx   eax, byte ptr &#91;esi&#93;<br />    mov     ecx, eax<br />    shr     al, 4<br /><br />    DPrintValH eax, &quot;bits for bitrate&#40;hex&#41;&quot;<br />    movsx   eax, word ptr &#91;edx&#93;&#91;2*eax&#93;<br />    DPrintValD eax, &quot;curbitrate&quot;<br /><br />    cmp     eax, -1<br />    je      _gotonextframe ;_invalid !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!<br />    sub     esi, 2 ;point back to start of frame, as framesize is added later<br />    ; eax is bitrate<br />    add     totalbitrate, eax<br />    mov     edx, &#91;edi&#93;.bitrate<br />    cmp     edx, -1<br />    jne     _bitrate_already_set<br />    mov     &#91;edi&#93;.bitrate, eax<br />    jmp     _fixed_bitrate_so_far<br />    _bitrate_already_set&#58;<br />    cmp     eax, edx        ;current bitrate same as set bitrate?<br />    je      _fixed_bitrate_so_far<br />    ;variable bitrate<br />    and     &#91;edi&#93;.bitrate, 0    ;set to zero indicating variable bitrate<br />    ;fall through...<br />    _fixed_bitrate_so_far&#58;<br /><br />    xor     edx, edx ; init framesize to zero<br />    shr     ecx, 2<br />    setc    dl       ;last bit shifted out is padding bit,<br />                     ; if set increase framesize by one<br /><br />    and     ecx, 3<br /><br />    cmp     ecx, 3<br />    jne     @F<br />    inc     esi<br />    cmp     esi, ebx<br />    jae     _done<br />    jmp _gotonextframe<br />    @@&#58;<br /><br />    shl     ecx, 1<br />    add     ecx, srTablePtr<br />    movzx   ecx, word ptr &#91;ecx&#93;<br />    mov     &#91;edi&#93;.frequency, ecx<br />    DPrintValD ecx, &quot;cur freq&quot;<br />    ;FrameSize = 144 * BitRate / SampleRate + Padding<br />    ; eax = bitrate&#40;in kbps, needs to be converted to bps yet&#41;<br />    ; ecx = sampleRate<br />    ; edx = padding<br />    ; framesize = 144 * eax / ecx + edx<br />    DPrintValD edx, &quot;padding&quot;<br />    push    edx<br />    mov     edx, 144*1000<br />    cmp     srTablePtr, offset samplerateTableV1<br />    je      @F<br />    mov     edx, 72*1000<br />    @@&#58;<br />    mul     edx<br />    div     ecx<br />    pop     edx<br />    add     edx, eax<br />    DPrintValD edx, &quot;framesize&quot;<br />    ; edx is framesize now<br /><br />    add     esi, edx<br />    cmp     esi, ebx<br />    jae     _done   ; frame bigger than file, file is cut<br /><br />    inc     &#91;edi&#93;.nFrames<br />    jmp     _gotonextframe<br /><br /><br />    _invalid&#58;<br />    DPrint &quot;---invalid, exit---&quot;<br />    xor     eax, eax<br />    jmp     _exit<br /><br />    _done&#58;<br />    mov     ebx, 1000<br /><br />    mov     ecx, &#91;edi&#93;.nFrames  ;get number of frames<br />    mov     eax, &#91;edi&#93;.bitrate  ;get bitrate in kbps<br />    mul     ebx                 ;*1000, in bpsd<br />    mov     &#91;edi&#93;.bitrate, eax  ;set new bitrate, in bps<br />    or      eax, eax            ;bitrate zero?<br />    jnz     _fixed_bitrate      ;if not, fixed<br />    mov     eax, totalbitrate   ;else, variable. get total &#40;sum of all bitrates&#41;<br />    mul     ebx                 ;multiple by 1000<br />    or      ecx, ecx            ;no frames?<br />    jz      _invalid            ;bye<br />    div     ecx                 ;divide by number of frames<br />    neg     eax                 ;negate, indicating a variable bitrate<br />    mov     &#91;edi&#93;.bitrate, eax<br />    _fixed_bitrate&#58;<br /><br />    ; 1152 = # of samples in one frame for layer 3 mpegs<br /><br />    mov     ebx, 1152*1000<br />    mov     eax, &#91;edi&#93;.nFrames<br />    xor     edx, edx<br />    mul     ebx<br />    mov     ebx, &#91;edi&#93;.frequency<br />    or      ebx, ebx<br />    jz      _invalid<br />    div     ebx<br />    mov     &#91;edi&#93;.fileLength, eax   ;set filelengths, in msecs<br /><br />    ;calculate time<br />    ;bitrate<br />    assume  edi&#58;nothing<br />    xor     eax, eax<br />    inc     eax<br /><br />    _exit&#58;<br />    push    eax<br /><br />    mov     eax, pMap<br />    or      eax, eax<br />    jz      @F<br />    invoke  UnmapViewOfFile, eax<br />    @@&#58;<br /><br />    mov     eax, hMap<br />    or      eax, eax<br />    jz      @F<br />    invoke  CloseHandle, eax<br />    @@&#58;<br /><br />    mov     eax, hFile<br />    or      eax, eax<br />    jz      @F<br />    invoke  CloseHandle, eax<br />    @@&#58;<br /><br />    pop     eax<br />ret<br />GetMP3Info endp<br /></code></pre><br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-22 14:04:24 by Thomas</div>
   </div>
   <div class="post" id="post-34477">
    <div class="subject"><a href="#post-34477">MP3 information</a></div>
    <div class="body">I've tested the function on 700 MB of MP3s, which had been loaded a few times before so they may have been in cache. It took 70 seconds (10MB/s). <br />So it takes quite some time to just calculate the length (msecs) of an mp3 file, but that's not strange as you need to read the header (first 4 bytes) of every frame. Most mp3's have a framesize of 417/418 bytes :(<br />I wonder if winamp reads it fully though. For fixed bitrate mp3s you may calculate the length by dividing the filesize by the length of one frame, but that's less accurate and wrong when a variable bitrate is used. If you browse a list of mp3s in the playlist of winamp you will see it takes some time before the lengths are displayed so it probably reads the whole file as well.<br /><br />I still don't understand how winamp calculates the number of frames in a file, it just can't be right or I'm missing something:<br /><div class="quote"><br />http://www.dv.co.yu/mpgscript/mpeghdr.htm</a>]<br /><br /><strong>How to calculate frame length</strong><br /><br />For Layer II &amp; III files use this formula: <br /><br />FrameLengthInBytes = 144 * BitRate / SampleRate + Padding <br /></div><br /><br />Now, I have one file with the following info (according to winamp):<br /><pre><code><br />........<br />Size&#58; 2188980 bytes<br />Length&#58; 273 seconds<br />&#91;b&#93;MPEG 2.0 layer 3<br />64kbit, 10523 frames&#91;/b&#93;<br />22050Hz Stereo<br />........<br /></code></pre><br />BitRate = 64000 (in bps), samplerate = 22050, padding is either 0 or 1 (varies per frame, but only 1 byte in difference at most).<br />Now let's assume none of the frames uses padding, which would be the smallest frames possible in this format. The size of these frames would be:<br />FrameLengthInBytes = 144 * 64000 / 22050 + 0<br />= 417 (using integer arithmetic, so rounded down)<br />Some of the frames may be 418 bytes, due to the padding, but let's assume every frame is as small as possible.<br />The maximum number of frames with these numbers would be: <br />max_frames = filesize/framesize = 2188980/417=<strong>5249</strong><br /><br />So at most, there can be 5249 frames in this file, how can winamp show there are 10523 frames??? :confused:<br /><br />Anyone knows more about this?<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-22 14:17:46 by Thomas</div>
   </div>
   <div class="post" id="post-34482">
    <div class="subject"><a href="#post-34482">MP3 information</a></div>
    <div class="body">Thomas, so shall we optimize for speed too?<br />I mean, not nessesarily implementation but also the algo itself.<br />Then, I might need mp3 formats to make anlysis of possible ways to calc.</div>
    <div class="meta">Posted on 2002-04-22 14:34:25 by The Svin</div>
   </div>
   <div class="post" id="post-34486">
    <div class="subject"><a href="#post-34486">MP3 information</a></div>
    <div class="body"><strong>Svin</strong>: Well the problem is you need file access and file access is slow. However you can simply calculate an estimated length of the mp3 using the filesize, but I like to have it precisely. <br /><br />I fixed the problem I described in my previous post, for MPEG2 files, the '144' used should be '72'. It works okay now (I edited the latest code post). The only thing I'm not sure about is if it works for MPEG2.5, but that isn't an official standard either. I don't have those files so I can't test it either.<br /><br />It's hard to find good info on the format, the link I showed you has some good information, but I've seen several sources telling different things..<br /><br />But for most MP3 it works now.<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-22 14:43:39 by Thomas</div>
   </div>
   <div class="post" id="post-34487">
    <div class="subject"><a href="#post-34487">MP3 information</a></div>
    <div class="body">If your mean by file access just opening file through CreateFile and setting pointers - it can not take so long as 70 secs.<br />The difference is what you do and how you do it with opened files.</div>
    <div class="meta">Posted on 2002-04-22 14:49:42 by The Svin</div>
   </div>
   <div class="post" id="post-34489">
    <div class="subject"><a href="#post-34489">MP3 information</a></div>
    <div class="body"><div class="quote"><br />If your mean by file access just opening file through CreateFile and setting pointers - it can not take so long as 70 secs.<br />The difference is what you do and how you do it with opened files. </div><br /><br />Well it's 70 seconds for 296 files totalling 700MB so it's not that bad. I've thought about using SetFilePointer/ReadFile instead of memory mapped files but I'm not sure it would be faster.<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-22 14:54:04 by Thomas</div>
   </div>
   <div class="post" id="post-34492">
    <div class="subject"><a href="#post-34492">MP3 information</a></div>
    <div class="body">Thomas, I can not say if it would be faster not having whole nessesary steps in mind.<br />But if you need to read just few parts of file data it's for sure faster.<br />You see mapping \ unmapping whole files <br />1. Yet need CreateFile<br />2. Involve a lot of work with for system for memory \ paging manipulation including hard usage of swap file, feeling and cleaning it. So system does a lot of write\read file itself with memory map files logic. It's usefull only when you do a whole file data work. And of course it is comfortable for programmer.<br />Not for speed.</div>
    <div class="meta">Posted on 2002-04-22 15:19:31 by The Svin</div>
   </div>
   <div class="post" id="post-34493">
    <div class="subject"><a href="#post-34493">id3v2</a></div>
    <div class="body">hi,<br /><br />i did, some time ago, some researchs in the id3v2 thingies that are inside mp3 and others.<br /><br />i didnt discovered anything, and give up really fast. but i coded that .inc file<br /><br />ancev<br /><br />ps: is nasm syntax</div>
    <div class="meta">Posted on 2002-04-22 15:21:10 by ancev</div>
   </div>
   <div class="post" id="post-34503">
    <div class="subject"><a href="#post-34503">MP3 information</a></div>
    <div class="body"><div class="quote">...It's usefull only when you do a whole file data work....</div><br /><br />To get the exact duration, you will need to read each frame, which means reading at ~400 bytes intervals. Not the full file but all the mapped pages are likely touched at that interval. <br /><br />Using readfile it would be enough to read only 4 bytes at each ~400 byte interval, unless the frame doesn't start there. In that case, a byte scan is needed to find the first sync bit sequence (11 bits set to 1, luckily always aligned to a byte).<br />This would cause a lot of readfiles for 4 bytes and a few for a whole sequence so I don't know if that would improve it much..<br /><br />Ancev: Thanks for the code I'll take a look at it.<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-22 16:04:56 by Thomas</div>
   </div>
   <div class="post" id="post-34591">
    <div class="subject"><a href="#post-34591">MP3 information</a></div>
    <div class="body">OK until I understood the whole algo let's proceed with size<br />first notion:<br /><pre><code><br />  xor ebx,ebx<br />  ....<br />  lea ecx,&#91;ebx&#93;&#91;5&#93; ; 3 bytes instead of 5 in mov ecx,5<br /></code></pre></div>
    <div class="meta">Posted on 2002-04-23 01:09:09 by The Svin</div>
   </div>
   <div class="post" id="post-35292">
    <div class="subject"><a href="#post-35292">MP3 information</a></div>
    <div class="body">Little bit.<br />1. mov edi,lpInfoStruct; push edi;...; pop edi =&gt; mov edi,lpInfoStruct; ...; mov edi,lpInfoStruct - one byte greate, but smaller memory access and instructions.<br />2. call CreateFileMapping; mov hMap,eax; test eax,eax; je _invalid. Remove mov hMap,ebx above. Same for pMap.<br />3. Move mov hFile,ebx after CreateFile for more memory access time locality.<br />4. For sequenced files use FILE_FLAG_SEQUENTIAL_SCAN, FILE_FLAG_NO_BUFFERING.<br />5. Use ReadFile with reading several frames.<br />6. or eax,eax - source of dependens for follow instruction, use test eax,eax instead.<br />7. or eax,eax; je label - jecxz label replace is shorter.<br />8. Divide by zero blocking may be perform early.</div>
    <div class="meta">Posted on 2002-04-27 10:53:16 by Nexo</div>
   </div>
   <div class="post" id="post-35300">
    <div class="subject"><a href="#post-35300">Re: MP3 information</a></div>
    <div class="body">Thanks for your suggestions, though I have a few comments:<br /><br /><div class="quote"><br />1. mov edi,lpInfoStruct; push edi;...; pop edi =&gt; mov edi,lpInfoStruct; ...; mov edi,lpInfoStruct - one byte greate, but smaller memory access and instructions.</div><br />Well I wanted to optimize for size, as the speed is almost completely determined by the file I/O speed..<br /><br /><div class="quote"><br />2. call CreateFileMapping; mov hMap,eax; test eax,eax; je _invalid. Remove mov hMap,ebx above. Same for pMap.<br /></div><br />I can't do that because hMap and pMap need to be intialized to zero intially (I used ebx because mov hMap, ebx is smaller than mov/and hMap, 0). This because of the error handling (_invalid). It only needs to close the file/map/mappointer handles if they are actually set. <br /><br /><div class="quote">5. Use ReadFile with reading several frames.</div><br />That might be faster but is more complex, and I'm lazy :grin:<br /><br />The other things are useful, thanks,<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-27 11:54:23 by Thomas</div>
   </div>
   <div class="post" id="post-35312">
    <div class="subject"><a href="#post-35312">Re: Re: MP3 information</a></div>
    <div class="body"><div class="quote"><em>Originally posted by Thomas </em><br /><br />I can't do that because hMap and pMap need to be intialized to zero intially (I used ebx because mov hMap, ebx is smaller than mov/and hMap, 0). This because of the error handling (_invalid). It only needs to close the file/map/mappointer handles if they are actually set. <br /><br /></div><br /><br />But, if add _invalid1, _invalid2, ... and skip additional checking for handles. Optimize for size ;)<br /><pre><code><br /><br />    mov &#91;dword esp&#93;,1 ; fixup result<br /> _exit&#58; ; push 0 above<br />    invoke  UnmapViewOfFile,pMap<br />_invalid1&#58;<br />    invoke  CloseHandle,hMap<br />_invalid2&#58;<br />    invoke  CloseHandle,hFile<br />    pop     eax<br />    ret<br /></code></pre></div>
    <div class="meta">Posted on 2002-04-27 13:22:17 by Nexo</div>
   </div>
   <div class="post" id="post-35578">
    <div class="subject"><a href="#post-35578">MP3 information</a></div>
    <div class="body">Hi,<br /><br />I've translated to Nasm syntax the second source Thomas posted, to use it as a library for PureBasic programs. Just wanted to ask if there was something wrong in doing so (cool proc, BTW!).<br /><br />Thanks,</div>
    <div class="meta">Posted on 2002-04-29 10:41:56 by El_Choni</div>
   </div>
   <div class="post" id="post-35582">
    <div class="subject"><a href="#post-35582">MP3 information</a></div>
    <div class="body">No problem :), but why didn't you assemble the MASM source into a library directly?<br /><br />Thomas</div>
    <div class="meta">Posted on 2002-04-29 11:05:40 by Thomas</div>
   </div>
   <div class="post" id="post-35583">
    <div class="subject"><a href="#post-35583">MP3 information</a></div>
    <div class="body">I'm not sure, but I think that PureBasic can only use Nasm's .obj for its libraries (yes, I'm a newbie ;).<br /><br />Tthanks a lot. Bye,</div>
    <div class="meta">Posted on 2002-04-29 11:14:16 by El_Choni</div>
   </div>
   <div class="post" id="post-40204">
    <div class="subject"><a href="#post-40204">MP3 information</a></div>
    <div class="body">One thing I didn't notice until today is that I used a multiplier of 1000 to convert from kbps (kilo bits per second) to bps (bits per second). Should I use 1024 here instead? That would be more logical but I couldn't find a confirmation of this.<br /><br />Thomas<br /><br />P.S. the latest version of this procedure is available at the snippet library on my site.</div>
    <div class="meta">Posted on 2002-05-27 09:24:47 by Thomas</div>
   </div>
<form class="pagination" action="../" method="get"><a href="../?id=4895&amp;page=1" style="visibility:hidden;">&laquo;</a><a href="../?id=4895&amp;page=0" style="visibility:hidden;">&lt;</a><input type="hidden" name="id" value="4895" /><input type="number" name="page" min="1" max="2" step="1" value="1" onchange="this.form.submit();" /><a href="../?id=4895&amp;page=2">&gt;</a><a href="../?id=4895&amp;page=2">&raquo;</a></form>  </div>
 </body>
</html>