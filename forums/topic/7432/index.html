<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>Message dispatcher system - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=7432" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=5">Algorithms &amp; Source Code</a> &raquo; <a href="../?id=7432">Message dispatcher system</a></p>
   <div class="post" id="post-54026">
    <div class="subject"><a href="#post-54026">Message dispatcher system</a></div>
    <div class="body">Hello, this is a message dispatcher system written in MASM macro.<br /><br />It automatically builds a table for a window class, provides a dispatcher for that class to load into a window class's lpfnWndProc, and can support superclassing, subclassing, and dialog boxes as well as ordinary window procs.<br /><br />Hope you like it, here is a sample of code written for it:<br /><pre><code><br />TITLE   Dispatch System test program.<br /><br />.386<br />.MODEL FLAT, STDCALL<br />OPTION CASEMAP&#58;NONE<br /><br /><br />include evrythng.inc<br />include dispatch.inc<br />;include \masm32\include\debug.inc<br />;includelib \masm32\lib\debug.lib<br /><br />IDM_TEST        EQU 1<br />IDM_HELLO       EQU 2<br />IDM_GOODBYE     EQU 3<br />IDM_EXIT        EQU 4<br /><br />IDC_EDIT        EQU 3000<br />IDC_BUTTON      EQU 3001<br />IDC_EXIT        EQU 3002<br /><br /><br />        ;=================<br />        ; Local prototypes<br />        ;=================<br />        WinMain PROTO &#58;DWORD,&#58;DWORD,&#58;DWORD,&#58;DWORD<br /><br />;These are the dispatchers created by dispatch.inc<br />        TestDialogWindowProc    PROTO<br />        MainWindowProc          PROTO<br /><br />;These are the message processors we defined for the 'Main' window class<br />        MainWM_DESTROYWindowProc        ProtoWindowProc<br />        MainWM_PAINTWindowProc          ProtoWindowProc<br />        MainWM_LBUTTONDOWNWindowProc    ProtoWindowProc<br />        MainWM_CHARWindowProc           ProtoWindowProc<br />        MainWM_COMMANDWindowProc        ProtoWindowProc<br /><br />;the following lines causes WM_RBUTTONDOWN messages to be sent to<br />;MainWM_LBUTTONDOWNWindowProc<br />MainWM_RBUTTONDOWNWindowProc    textequ &lt;MainWM_LBUTTONDOWNWindowProc&gt;<br /><br />;These are the message processors we defined for the 'TestDialog' window class<br />        TestDialogWM_INITDIALOGWindowProc       ProtoWindowProc<br />        TestDialogWM_CLOSEWindowProc            ProtoWindowProc<br />        TestDialogWM_COMMANDWindowProc          ProtoWindowProc<br /><br /><br />.DATA<br />CommandLine     dd 0<br />hWndMain        dd 0<br />hInstance       dd 0<br />hIcon           dd 0<br />AppName         db &quot;Test Program&quot;,0<br />MainWndClass    db &quot;TestWindowClass&quot;,0<br />TestMenu        db &quot;TestMenu&quot;,0<br />TestDialogTemplate      db &quot;TestDialog&quot;,0<br />char            WPARAM 20h<br /><br />.data?<br />MouseLocation   dd ?,?<br />MouseClicked    dd ?<br /><br />.data<br />;Window Class Registration structure&#40;s&#41;<br />wcMain          WNDCLASSEX\<br />               &lt;sizeof WNDCLASSEX,\<br />                CS_HREDRAW or CS_VREDRAW,\<br />                MainWindowProc,\<br />                NULL,\<br />                NULL,\<br />                NULL,\<br />                NULL,\<br />                NULL,\<br />                COLOR_WINDOW + 1,\<br />                TestMenu,\<br />                MainWndClass,\<br />                NULL&gt;<br /><br />.data<br />TestString      db &quot;Hello Cruel World!  I'm in an edit box now!&quot;,0<br />HelloString     db &quot;Hello World!&quot;,0<br />GoodbyeString   db &quot;Goodbye Carlo!&quot;,0<br /><br />.CODE<br />DefDialogBoxProc        proc<br />        xor     eax,eax<br />        ret     10h<br />DefDialogBoxProc        endp<br /><br /><br />start&#58;<br />        ;Initialization section<br />        Invoke  GetModuleHandle,        NULL<br />        mov     hInstance,eax<br />        Invoke  GetCommandLine<br />        mov     CommandLine,eax<br /><br />        ;Initializators for the 'Main' and 'TestDialog' dispatchers<br />        ;required for compatibility with ddspatch.inc<br />        InitializatorFor    Main<br />        InitializatorFor    TestDialog,DialogBox<br /><br />        ;WinMain section<br />        Invoke  WinMain,                hInstance,NULL,CommandLine,SW_SHOWDEFAULT<br /><br />        ;ShutDown section<br />        Invoke  ExitProcess,            eax<br /><br />;WinMain - Main procedure of the program<br />WinMain proc hInst&#58;HINSTANCE,hPrevInst&#58;HINSTANCE,CmdLine&#58;LPSTR,CmdShow&#58;DWORD<br />        LOCAL   msg&#58;MSG<br />        ;Window Class Registration section<br />        ;-----------------------wcMain<br />        ;Load uninitialized data<br />        mov     eax,hInst<br />        mov     wcMain.hInstance,eax<br />        Invoke  LoadIcon,               NULL,IDI_APPLICATION<br />        mov     wcMain.hIcon,eax<br />        mov     wcMain.hIconSm,eax<br />        Invoke  LoadCursor,             NULL,IDC_ARROW<br />        mov     wcMain.hCursor,eax<br />        Invoke  RegisterClassEx,        addr wcMain<br /><br />        ;application init section<br />        mov     MouseClicked,FALSE<br /><br />        ;Window Creation section<br />        ;       CreateWindowEx,         NULL, Window Class Name, Window Title,\<br />        ;                               Window Type,\<br />        ;                               x,y,\<br />        ;                               cx,cy,\<br />        ;                               Parent, Menu, Instance, Parameters<br />        ;-----------------------Create Main Window<br />        Invoke  CreateWindowEx,         NULL,addr MainWndClass,addr AppName,\<br />                                        WS_OVERLAPPEDWINDOW,\<br />                                        CW_USEDEFAULT,CW_USEDEFAULT,\<br />                                        CW_USEDEFAULT,CW_USEDEFAULT,\<br />                                        NULL,NULL,hInst,NULL<br />        mov     hWndMain,eax<br /><br />        Invoke  ShowWindow,             hWndMain,SW_SHOWNORMAL<br />        Invoke  UpdateWindow,           hWndMain<br /><br />        ;Message Loop section<br />MessageLoopBegins&#58;<br />        Invoke  GetMessage,             addr msg,NULL,0,0<br />        cmp     eax,0<br />        jz      MessageLoopEnds<br />        Invoke  TranslateMessage,       addr msg<br />        Invoke  DispatchMessage,        addr msg<br />        jmp     MessageLoopBegins<br />MessageLoopEnds&#58;<br /><br />        ;Closing Section<br /><br />        ;fin<br />        Return  msg.wParam<br />WinMain endp<br /><br /><br />;--------------------------------------------------------------------------Main<br />;'Main' Window Class WindowProc's<br />;------------------------------------------------------------------------------<br /><br />        DispatcherFor   Main<br /><br /><br />MainWM_DESTROYWindowProc        WindowProc<br />        Invoke  PostQuitMessage,        NULL<br />        Return  0<br />MainWM_DESTROYWindowProc        endp<br /><br /><br />MainWM_PAINTWindowProc          WindowProc<br />        LOCAL   hDC&#58;HDC<br />        LOCAL   ps&#58;PAINTSTRUCT<br />        LOCAL   ClientRect&#58;RECT<br /><br />        Invoke  BeginPaint,             hWnd,addr ps<br />        mov     hDC,eax<br />        Invoke  GetClientRect,          hWnd,addr ClientRect<br /><br />;-----------Meat of the Paint Procedure HERE!<br />        cmp     MouseClicked,TRUE<br />        jne     notMouseClicked<br />        szText  text,&quot;Mouse was clicked here...&quot;<br />        Invoke  TextOut,                hDC,MouseLocation,MouseLocation&#91;4&#93;,addr text,25<br />notMouseClicked&#58;<br />        Invoke  TextOut,                hDC,0,0,addr char,1<br /><br />        Invoke  EndPaint,               hWnd,addr ps<br />        xor     eax,eax<br />        ret<br />MainWM_PAINTWindowProc          endp<br /><br />MainWM_LBUTTONDOWNWindowProc    WindowProc<br />        mov     MouseClicked,TRUE<br />        movsx   eax,word ptr lParam<br />        movsx   ebx,word ptr lParam&#91;2&#93;<br />        mov     MouseLocation,eax<br />        mov     MouseLocation&#91;4&#93;,ebx<br />        Invoke  InvalidateRect,         hWnd,NULL,TRUE<br />        xor     eax,eax<br />        ret<br />MainWM_LBUTTONDOWNWindowProc    endp<br /><br />MainWM_CHARWindowProc   WindowProc<br />        push    wParam<br />        pop     char<br />        Invoke  InvalidateRect,         hWnd,NULL,TRUE<br />        xor     eax,eax<br />        ret<br />MainWM_CHARWindowProc   endp<br /><br />MainWM_COMMANDWindowProc        WindowProc<br />        movzx   eax,word ptr wParam<br />        cmp     eax,IDM_TEST<br />        jne     EndIDM_TEST<br />StartIDM_TEST&#58;<br />        Invoke  DialogBoxParam,         hInstance,addr TestDialogTemplate,\<br />                                        hWnd,offset TestDialogWindowProc,NULL<br />        jmp     getout<br />EndIDM_TEST&#58;<br /><br />        cmp     eax,IDM_HELLO<br />        jne     EndIDM_HELLO<br />StartIDM_HELLO&#58;<br />        Invoke  MessageBox,             NULL,addr HelloString,offset AppName,MB_OK<br />        jmp     getout<br />EndIDM_HELLO&#58;<br /><br />        cmp     eax,IDM_GOODBYE<br />        jne     EndIDM_GOODBYE<br />StartIDM_GOODBYE&#58;<br />        Invoke  MessageBox,             NULL,addr GoodbyeString,offset AppName,MB_OK<br />        jmp     getout<br />EndIDM_GOODBYE&#58;<br /><br />        cmp     eax,IDM_EXIT<br />        jne     EndIDM_EXIT<br />StartIDM_EXIT&#58;<br />        Invoke  DestroyWindow,          hWnd<br />EndIDM_EXIT&#58;<br /><br />getout&#58;<br />        xor     eax,eax<br />        ret<br />MainWM_COMMANDWindowProc        endp<br /><br /><br />;--------------------------------------------------------------------TestDialog<br />;TestDialog's WindowProcs<br />;------------------------------------------------------------------------------<br /><br /><br />        DispatcherFor   TestDialog,DialogBox<br /><br /><br />TestDialogWM_INITDIALOGWindowProc       WindowProc<br />        Invoke  GetDlgItem,             hWnd,IDC_EDIT<br />        Invoke  SetFocus,               eax<br />        mov     eax,TRUE<br />        ret<br />TestDialogWM_INITDIALOGWindowProc       endp<br /><br /><br />TestDialogWM_CLOSEWindowProc            WindowProc<br />        Invoke  EndDialog,              hWnd,NULL<br />        mov     eax,TRUE<br />        ret<br />TestDialogWM_CLOSEWindowProc            endp<br /><br />TestDialogWM_COMMANDWindowProc          WindowProc<br />        movzx   edx,word ptr wParam+2<br />        movzx   eax,word ptr wParam<br />        cmp     edx,BN_CLICKED<br />        jne     ignoreit<br />        cmp     eax,IDC_EXIT<br />        jne     notexit<br />        Invoke  SendMessage,            hWnd,WM_CLOSE,NULL,NULL<br />        jmp     ignoreit<br />notexit&#58;<br />        cmp     eax,IDC_BUTTON<br />        jne     ignoreit<br />        Invoke  SetDlgItemText,         hWnd,IDC_EDIT,addr TestString<br />ignoreit&#58;<br />        mov     eax,TRUE<br />        ret<br />TestDialogWM_COMMANDWindowProc          endp<br /><br /><br />end start<br /><br /></code></pre><br /><br />please read more about it in the documentation.</div>
    <div class="meta">Posted on 2002-08-18 19:48:23 by AmkG</div>
   </div>
   <div class="post" id="post-54029">
    <div class="subject"><a href="#post-54029">Message dispatcher system</a></div>
    <div class="body">Hey, cool.  :cool: :alright:<br /><br />Very useful.  Good job.  ;)</div>
    <div class="meta">Posted on 2002-08-18 19:56:34 by iblis</div>
   </div>
   <div class="post" id="post-54031">
    <div class="subject"><a href="#post-54031">Message dispatcher system</a></div>
    <div class="body">Gee, thanks.<br /><br /><br />BTW there are two versions of the dispatcher, one is dispatch.inc, the other is ddspatch.inc, dispatch.inc creates the dispatcher table at compile-time, ddspatch.inc creates the table at run-time.  Dispatch.inc thus makes a larger exe file, but with no overhead, while ddspatch.inc makes a smaller exe file, but requires table-creation code at init (the 'InitializatorFor' macro).<br /><br />Anyway the sample code above will work with either dispatch.inc or ddspatch.inc, you only need to change the include line.</div>
    <div class="meta">Posted on 2002-08-18 20:17:59 by AmkG</div>
   </div>
  </div>
 </body>
</html>