<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>Problem with Textscroller on WS_EX_LAYERED window - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=28974" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=3">MAIN</a> &raquo; <a href="../?id=28974">Problem with Textscroller on WS_EX_LAYERED window</a></p>
   <div class="post" id="post-204835">
    <div class="subject"><a href="#post-204835">Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body">I coded an textscrollengine. it works fine on usual windows. but when i use it on windows with WS_EX_LAYERED style the scroller is very slow and flickers.<br /><br />Any ideas?<br /><br />i attached the sourcecode.<br /></div>
    <div class="meta">Posted on 2008-02-20 19:51:17 by diablo2oo2</div>
   </div>
   <div class="post" id="post-204840">
    <div class="subject"><a href="#post-204840">Re: Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body">clear the question.<br /><br />i don&#039;t know how to figure out.</div>
    <div class="attachments">Attachments:
     <ul>
      <li><a href="../../attachments/?id=2559" target="_blank">scroller.zip</a></li>
     </ul>
    </div>
    <div class="meta">Posted on 2008-02-22 00:56:31 by dcskm4200</div>
   </div>
   <div class="post" id="post-204859">
    <div class="subject"><a href="#post-204859">Re: Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body">problem is solved. if you use the textscroller with a transparent window, you should add <strong>SetLayeredWindowAttributes</strong> inside the scrollerloop:<br /><br /><pre><code>	@loop:<br />	<br />	.if .scroll_pause==0<br />	<br />		invoke BitBlt,local_hdc_text,0,0,.scroll_width,local_scroll_height,local_hdc_window_copy,.scroll_x,.scroll_y,SRCCOPY<br />		<br />		invoke TextOut,local_hdc_text,ebx,0,.scroll_text,local_text_len<br />		<br />		invoke BlendBitmap,local_hdc_text,local_hdc_window_copy,local_scroll_height,.scroll_width,.scroll_x,.scroll_y,.scroll_textcolor<br />		<br />		invoke BitBlt,local_hdc_window,.scroll_x,.scroll_y,.scroll_width,local_scroll_height,local_hdc_text,0,0,SRCCOPY			<br /><br />		dec ebx<br />	<br />		.if ebx==local_text_endpos<br />			;---reset text position to begining---<br />			mov ebx,.scroll_width<br />		.endif<br />		<br />		;---important for transparent window---<br />		movzx eax,.scroll_alpha<br />		.if al!=0 &amp;&amp; al!=255<br />			invoke SetLayeredWindowAttributes,.scroll_hwnd,0,eax,LWA_ALPHA<br />		.endif	<br />	.endif<br />	<br />	invoke Sleep,30<br />	<br />	jmp @loop</code></pre></div>
    <div class="meta">Posted on 2008-02-25 10:52:44 by diablo2oo2</div>
   </div>
   <div class="post" id="post-204861">
    <div class="subject"><a href="#post-204861">Re: Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body"><strong>Thanks.</strong></div>
    <div class="meta">Posted on 2008-02-25 23:09:09 by dcskm4200</div>
   </div>
   <div class="post" id="post-204862">
    <div class="subject"><a href="#post-204862">Re: Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body">thanks diablo2oo2&nbsp;  :D</div>
    <div class="meta">Posted on 2008-02-26 09:46:15 by Ahmed18</div>
   </div>
   <div class="post" id="post-204868">
    <div class="subject"><a href="#post-204868">Re: Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body">Very pretty</div>
    <div class="meta">Posted on 2008-02-26 11:25:18 by JimmyClif</div>
   </div>
   <div class="post" id="post-204871">
    <div class="subject"><a href="#post-204871">Re: Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body">Here is my final version of TextScroll engine with example sourcecode<br /><br /><div class="quote"><strong>Download here</strong><br />http://diablo2oo2.di.funpic.de/downloads/textscroller.rar<br />http://free.pages.at/d2k2/downloads/textscroller.rar</div></div>
    <div class="attachments">Attachments:
     <ul>
      <li><a href="../../attachments/?id=2561" target="_blank">textscroller.rar</a></li>
     </ul>
    </div>
    <div class="meta">Posted on 2008-02-26 18:44:39 by diablo2oo2</div>
   </div>
   <div class="post" id="post-206350">
    <div class="subject"><a href="#post-206350">Re: Problem with Textscroller on WS_EX_LAYERED window</a></div>
    <div class="body">I updated the code. Now the scrolltext engine doesnt use GetPixel &amp; SetPixel API.<br /><br /><div class="quote"><strong>Download here</strong><br />http://diablo2oo2.di.funpic.de/downloads/textscroller.rar<br />http://free.pages.at/d2k2/downloads/textscroller.rar</div><br /><br /><br /><pre><code><br />;******************************************************************************<br />;* STRUCTURE&nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; *<br />;******************************************************************************<br /><br />SCROLLER_STRUCT struct<br />	scroll_hwnd		dd ? ;handle of window where to draw<br />	scroll_text		dd ? ;pointer scrolltext<br />	scroll_x		dd ? ;x position<br />	scroll_y		dd ? ;y position<br />	scroll_width		dd ? ;width of scroller<br />	scroll_hFont		dd ? ;Handle of Font<br />	scroll_textcolor	dd ? ;example: 00F7DDCCh (00BBGGRR)<br />	scroll_alpha		db ? ;value for transparency (if using it on WS_EX_LAYERED window)<br />	scroll_wait		dd ? ;wait time (milliseconds) before draw scrolltext<br />	scroll_pause		db ? ;dont modify this! Use PauseScroller function!<br />SCROLLER_STRUCT ends<br /><br /><br /><br />;******************************************************************************<br />;* PROTOTYPES&nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp;  *<br />;******************************************************************************<br /><br />;---public functions---<br />CreateScroller			PROTO :DWORD<br />PauseScroller			PROTO :DWORD</code></pre><br /><br /><pre><code><br />;******************************************************************************<br />;* diablo2oo2&#039;s Textscroller Engine&nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp;  *<br />;******************************************************************************<br /><br /><br />include	Textscroller.inc<br /><br /><br />;---private functions of scroller engine---<br />ScrollThread			PROTO :DWORD<br />BlendBitmap			PROTO :DWORD,:DWORD,:DWORD,:DWORD<br />BlendPixel			PROTO :DWORD,:DWORD,:DWORD<br />PercentValue			PROTO :DWORD,:DWORD<br />PercentColor			PROTO :DWORD,:DWORD<br />GetDIBPixel			PROTO :DWORD,:DWORD,:DWORD,:DWORD,:DWORD<br />SetDIBPixel			PROTO :DWORD,:DWORD,:DWORD,:DWORD,:DWORD,:DWORD<br /><br /><br />;******************************************************************************<br />;* CODE&nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp;  *<br />;******************************************************************************<br /><br />.code<br />;---public---<br />CreateScroller proc _scrollstruct:dword<br />	<br />	LOCAL ThreadID		:DWORD<br />	<br />	fn CreateThread,0,0,addr ScrollThread,_scrollstruct,0,addr ThreadID<br />	fn CloseHandle,eax<br />	<br />	ret<br />CreateScroller endp<br /><br /><br />PauseScroller proc _scrollstruct:dword<br />	<br />	mov eax,_scrollstruct<br />	assume eax:ptr SCROLLER_STRUCT<br />	<br />	mov cl,.scroll_pause<br />	<br />	.if cl==0<br />		inc cl<br />	.else<br />		dec cl<br />	.endif		<br />	<br />	mov .scroll_pause,cl<br />		<br />	assume eax:nothing<br />	<br />	ret<br />PauseScroller endp<br /><br /><br />;---private---<br />ScrollThread proc _scrollstruct:dword<br />	<br />	LOCAL local_hdc_window		:DWORD<br />	LOCAL local_hdc_window_copy	:DWORD<br />	LOCAL local_hdc_text		:DWORD<br />	<br />	LOCAL local_scroll_height	:DWORD<br />	<br />	LOCAL local_text_len		:DWORD<br />	LOCAL local_text_width		:DWORD<br />	LOCAL local_text_endpos		:DWORD<br />	<br />	LOCAL local_pbmi		:BITMAPINFO<br />	LOCAL local_pixels_window_copy	:DWORD<br />	LOCAL local_pixels_text		:DWORD<br />	<br />	LOCAL local_sz 			:SIZEL<br />	<br />	<br />	;---scroller structure---<br />	mov esi,_scrollstruct<br />	assume esi:ptr SCROLLER_STRUCT<br />	<br />	<br />	;---wait before draw---<br />	mov eax,.scroll_wait<br />	.if eax&lt;500<br />		mov eax,500<br />	.endif	<br />	fn Sleep,eax		;important!<br />	<br />	<br />	;---Textlen---<br />	fn lstrlen,.scroll_text<br />	mov local_text_len,eax<br />	<br />	<br />	<br />	;---get window dc---<br />	fn GetDC,.scroll_hwnd<br />	mov local_hdc_window,eax<br />	<br />	<br />	<br />	;---HDC for text---<br />	fn GetDC,0<br />	fn CreateCompatibleDC,eax<br />	mov local_hdc_text,eax<br />	<br />	;---use custom font---<br />	fn SelectObject,eax,.scroll_hFont<br />	<br />	;---get Textheight and width---<br />	fn GetTextExtentPoint,local_hdc_text,.scroll_text,local_text_len,addr local_sz<br />	.if eax==TRUE <br />	<br />		m2m local_scroll_height,local_sz.y<br />		m2m local_text_width,local_sz.x<br />		<br />		<br />		;---..hdc for text---<br />		fn CreateCompatibleBitmap,local_hdc_window,.scroll_width,local_scroll_height<br />		fn SelectObject,local_hdc_text,eax<br />		<br />		<br />		lea edi,local_pbmi<br />		assume edi:ptr BITMAPINFO<br />		<br />		fn RtlZeroMemory,edi,sizeof BITMAPINFO<br />		<br />		mov .bmiHeader.biSize , sizeof BITMAPINFOHEADER<br />		m2m .bmiHeader.biWidth , .scroll_width<br />		m2m .bmiHeader.biHeight , local_scroll_height<br />		mov .bmiHeader.biPlanes , 1<br />		mov .bmiHeader.biBitCount , 32	 <br />		mov .bmiHeader.biCompression , BI_RGB<br />		<br />		<br />		fn CreateDIBSection,local_hdc_text,edi,DIB_RGB_COLORS,addr local_pixels_text,0,0<br />		fn SelectObject,local_hdc_text,eax<br />		assume edi:nothing<br />		<br />		<br />		;---HDC for windowcopy---<br />		fn GetDC,0<br />		fn CreateCompatibleDC,eax<br />		mov local_hdc_window_copy,eax<br />		<br />		<br />		<br />		;---window dib &amp; window copy---<br />		lea edi,local_pbmi<br />		<br />		fn CreateDIBSection,local_hdc_window_copy,edi,DIB_RGB_COLORS,addr local_pixels_window_copy,0,0<br />		fn SelectObject,local_hdc_window_copy,eax<br />		<br />		fn BitBlt,local_hdc_window_copy,0,0,.scroll_width,local_scroll_height,local_hdc_window,.scroll_x,.scroll_y,SRCCOPY<br />		<br />		<br />		;---Set Text Color---<br />		fn SetBkMode,local_hdc_text,TRANSPARENT<br />		fn SetTextColor,local_hdc_text,.scroll_textcolor<br />		<br />		<br />		;---for transparent windows---<br />		fn GetModuleHandle,&quot;user32.dll&quot;<br />		fn GetProcAddress,eax,&quot;SetLayeredWindowAttributes&quot;<br />		mov edi,eax<br />		<br />		;---calc endposition of text---<br />		xor eax,eax<br />		sub eax,local_text_width<br />		sub eax,8<br />		mov local_text_endpos,eax<br />		<br />		<br />		;---prepare loop---<br />		mov ebx,.scroll_width	;ebx=text position<br />		add ebx,4<br />		<br />		<br />		;---loop---<br />		@loop:<br />		<br />			.if .scroll_pause==0<br />				<br />				;---draw background for scroll gfx---<br />				fn BitBlt,local_hdc_text,0,0,.scroll_width,local_scroll_height,local_hdc_window_copy,0,0,SRCCOPY<br />				<br />				;---draw scrolltext on background---<br />				fn TextOut,local_hdc_text,ebx,0,.scroll_text,local_text_len<br />				<br />				;---fade text in and out---<br />				fn BlendBitmap,local_pixels_text,local_pixels_window_copy,local_scroll_height,.scroll_width<br />				<br />				;---draw scrolltext on window---<br />				fn BitBlt,local_hdc_window,.scroll_x,.scroll_y,.scroll_width,local_scroll_height,local_hdc_text,0,0,SRCCOPY			<br />		<br />				dec ebx<br />			<br />				.if ebx==local_text_endpos<br />					;---reset text position to begining---<br />					mov ebx,.scroll_width<br />				.endif<br />				<br />				<br />				;---important for transparent window---<br />				.if edi!=0<br />					movzx eax,.scroll_alpha<br />					.if al!=0 &amp;&amp; al!=255<br />						Scall edi,.scroll_hwnd,0,eax,LWA_ALPHA<br />					.endif<br />				.endif<br />			.endif<br />			<br />			fn Sleep,30<br />		<br />		jmp @loop<br />		<br />	.endif<br />	<br />	assume esi:nothing<br />	<br />	ret<br />ScrollThread endp<br /><br /><br />;---Blend Routine---<br />align 16<br />BlendBitmap proc uses esi edi ebx _text_dib, _window_copy_dib, _height, _width<br />	<br />	LOCAL local_blendvalue	:DWORD<br />	LOCAL local_fadeout_pos	:DWORD<br />	<br />	.const<br />	FADE_WIDTH	equ 25<br />	FADE_STEP	equ 4<br />	<br />	.code<br />	mov eax,_width<br />	<br />	.if eax&gt;=2*FADE_WIDTH	;only works with minimum width<br />		<br />		<br />		;---calc x-coordinate where to start fade out---<br />		sub eax,FADE_WIDTH<br />		mov local_fadeout_pos,eax<br />		<br />		<br />		;---prepare loop--<br />		xor esi,esi		;x=width<br />		mov local_blendvalue,0<br />		<br />		<br />		;---blend loop---<br />		.while esi!=_width<br />			<br />			xor edi,edi	;y=height<br />			<br />			.while edi!=_height<br />				<br />				;---get pixel of scrolltext hdc---<br />				fn GetDIBPixel,esi,edi,_text_dib,_width,_height<br />				mov ebx,eax<br />					<br />				fn GetDIBPixel,esi,edi,_window_copy_dib,_width,_height<br />					<br />				fn BlendPixel,eax,ebx,local_blendvalue<br />					<br />				fn SetDIBPixel,esi,edi,_text_dib,_width,_height,eax<br /><br />				inc edi<br />			.endw<br />			<br />			<br />			;---for fading---<br />			.if 	esi&lt;FADE_WIDTH<br />				add local_blendvalue,FADE_STEP	;4 * 25pixel = 100 %<br />				<br />			.elseif esi==FADE_WIDTH<br />				;---skip non fading area and jump zo fadeout area---<br />				mov esi,local_fadeout_pos<br />					<br />			.elseif esi&gt;local_fadeout_pos<br />				sub local_blendvalue,FADE_STEP	;4 * 25pixel = 100 %<br />				<br />			.endif	<br />	<br />			inc esi	<br />		.endw<br />	.endif	<br />	<br />	ret<br />BlendBitmap endp<br /><br /><br /><br />align 16<br />GetDIBPixel proc _x,_y,_pDIBits,_width,_height<br /><br />	mov eax,_width<br />	mov ecx,_height<br /><br />	sub ecx,_y<br />	dec ecx<br />	mul ecx<br />	shl eax,2 ; adjust for DWORD size<br />	push eax ; push the result onto the stack<br /><br />	mov eax,_x<br />	shl eax,2 ; adjust for DWORD size<br />	pop ecx ; pop the scan line offset off the stack<br />	add eax,ecx<br /><br />	add eax,_pDIBits ; add the offset to the DIB bit<br />	mov eax,<br /><br />	RET<br />GetDIBPixel endp<br /><br /><br />align 16<br />SetDIBPixel proc _x,_y,_pDIBits,_width,_height,_color<br /><br />	mov eax,_width<br />	mov ecx,_height<br /><br />	cmp _x,eax<br />	jae @exit<br />	<br />	cmp _y,ecx<br />	jae @exit<br /><br />	sub ecx,_y<br />	dec ecx<br />	mul ecx<br />	shl eax,2 ; adjust for DWORD size<br />	mov edx,eax<br /><br />	mov eax,_x<br />	shl eax,2 ; adjust for DWORD size<br />	add eax,edx<br /><br />	add eax,_pDIBits ; add the offset to the DIB bit<br />	mov ecx,_color<br />	mov ,ecx<br /><br />	@exit:<br />	RET<br />SetDIBPixel endp<br /><br /><br />align 16<br />BlendPixel proc uses esi edi ebx _sourcepixel:dword,_overpixel:dword,_transparency:dword<br />	<br />	;---parameters---<br />	;_sourcepixel&nbsp; : Pixel of Backgroundimage<br />	;_overpixel&nbsp; &nbsp; : Pixel which overlaps the sourcepixel<br />	;_transparency : 5 - 90 %&nbsp; (using 100 % is stupid)<br />	<br />	;---Color Format---<br />	; 00 00 00 00<br />	; xx BB GG RR<br />	<br />	.if _transparency&lt;100<br />		<br />		mov eax,_overpixel<br />		.if eax!=_sourcepixel<br />			<br />			;---calc new colors of _sourcepixel---<br />			mov eax,100<br />			sub eax,_transparency<br />			<br />			fn PercentColor,_sourcepixel,eax<br />			mov ebx,eax<br />			<br />			<br />			;---calc new colors of _overpixel---	<br />			fn PercentColor,_overpixel,_transparency<br />			<br />			<br />			;---add each color---<br />			xor esi,esi<br />			<br />			.while esi!=3<br />				<br />				movzx edx,al<br />				movzx ecx,bl<br />				<br />				add edx,ecx<br />				.if edx&gt;255<br />					mov dl,255<br />				.endif<br />				<br />				mov al,dl<br />					<br />				ror eax,8<br />				ror ebx,8<br />				<br />				inc esi<br />			.endw<br />			<br />			rol eax,3*8<br />		.else	<br />			mov eax,_overpixel<br />		.endif	<br />	.else	<br />		mov eax,_overpixel<br />	<br />	.endif<br />	<br />	ret<br />BlendPixel endp<br /><br /><br />align 16<br />PercentValue proc _value:dword,_percent:dword<br /><br />	mov eax,_value<br />	<br />	mul _percent<br />	<br />	mov ecx,100<br />	<br />	xor edx,edx<br />	div ecx<br />	<br />	ret<br />PercentValue endp<br /><br /><br />align 16<br />PercentColor proc uses esi edi ebx _color:dword,_percent:dword<br />	<br />	;---reduce color by certain percent---<br />	<br />	mov ebx,_color<br />	<br />	;---Red--<br />	movzx eax,bl<br />	<br />	fn PercentValue,eax,_percent<br />	mov edi,eax<br />	<br />	<br />	;---Green---<br />	ror ebx,8<br />	movzx eax,bl<br />	fn PercentValue,eax,_percent<br />	<br />	ror edi,8 <br />	mov edx,edi<br />	mov dl,al<br />	mov edi,edx<br />	<br />	<br />	;---Blue---<br />	ror ebx,8<br />	movzx eax,bl<br />	fn PercentValue,eax,_percent<br />	<br />	ror edi,8 <br />	mov edx,edi<br />	mov dl,al<br />	mov edi,edx<br />	<br />	<br />	;---return new color value---<br />	rol edi,16<br />	mov eax,edi<br />	<br />	ret<br />PercentColor endp</code></pre><br /><br />------------------<br /><br /><strong>usage:</strong><br /><pre><code>...<br />	.if eax==WM_INITDIALOG<br />		<br />		;---make dialog transparent---	<br />		fn MakeDialogTransparentValue,hwnd,TRANSPARENT_VALUE<br />		<br />		<br />		;---fill scroll structure---<br />		m2m scr.scroll_hwnd,hwnd<br />		<br />		mov scr.scroll_text,chr$(&quot;Test 123 ... This is a scrolltext&quot;)<br />		<br />		mov scr.scroll_x,10<br />		mov scr.scroll_y,20<br />		<br />		mov scr.scroll_width,280<br />		<br />		fn lstrcpy,addr lf.lfFaceName,&quot;MS SANS SERIF&quot;<br />		mov lf.lfHeight,40<br />		mov lf.lfCharSet,DEFAULT_CHARSET	<br />		mov lf.lfQuality,ANTIALIASED_QUALITY<br />		fn CreateFontIndirect,addr lf		;returns handle of Font<br />		mov scr.scroll_hFont,eax<br />		<br />		mov scr.scroll_alpha,TRANSPARENT_VALUE<br />		mov scr.scroll_textcolor,00040302h<br />		<br />		fn CreateScroller,addr scr<br />...</code></pre></div>
    <div class="meta">Posted on 2008-11-24 14:59:39 by diablo2oo2</div>
   </div>
  </div>
 </body>
</html>