<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>Probs whith terminating a Prog, how to terminate them ? - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=11259" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=3">MAIN</a> &raquo; <a href="../?id=11259">Probs whith terminating a Prog, how to terminate them ?</a></p>
   <div class="post" id="post-84894">
    <div class="subject"><a href="#post-84894">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">Hi, I want to prog a Taskmanager.<br />Some Progs are a little stubbornly, can someone help me ?<br />This the source, and its only possible to terminate small aplication.<br /><br />terminatetemp db &quot;explorer.exe&quot;, 0<br />terminate1 db &quot;explorer.exe Smc.exe&quot;, 0<br /><br />.code<br />INVOKE lstrcpy ADDR terminatetemp, ADDR terminate1<br />INVOKE terminatefw<br /><br />terminatefw PROC<br />       push edi<br />       push eax<br />       push ecx<br />INVOKE lstrcpy, EDI, ADDR terminatetemp<br /><br />	mov	edi, eax<br />	xor	ecx, ecx<br />	invoke	lstrlen, eax<br />	.while ecx!=eax<br />		.if byte ptr ==&quot; &quot;<br />			inc	edi<br />			mov	, edi<br />			.break<br />		.endif<br />		inc	ecx<br />		inc	edi<br />	.endw	<br />	cmp	ecx, eax<br />	je	done<br /> 	mov	, sizeof uProcess<br />	invoke	CreateToolhelp32Snapshot, 2, 0<br />	mov	, eax<br />	invoke	Process32First, eax, ADDR uProcess<br />	.while eax<br />		xor	ecx, ecx<br />		lea	edi, <br />		mov	ebx, edi<br />		dec	ebx<br />		invoke	lstrlen, edi<br />		add	edi, eax<br />		.while edi!=ebx<br />			invoke	lstrcmpi, edi, <br />			.if !eax<br />				invoke	OpenProcess, PROCESS_TERMINATE, 1, <br />				invoke	TerminateProcess, eax, 0<br />				jmp	done<br />			.endif<br />			; optimize this<br />			dec	edi<br />		.endw<br />		invoke	Process32Next, , ADDR uProcess<br />	.endw<br /><br />done:	invoke	CloseHandle, <br />       pop edi<br />       pop eax<br />       pop ecx<br />            ret<br />terminatefw ENDP</div>
    <div class="meta">Posted on 2003-03-04 02:09:41 by Forginforcer</div>
   </div>
   <div class="post" id="post-84918">
    <div class="subject"><a href="#post-84918">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">&quot;terminatefw&quot;... did you copy this code from somewhere else, or are you out to do bad stuff?</div>
    <div class="meta">Posted on 2003-03-04 04:57:33 by f0dder</div>
   </div>
   <div class="post" id="post-84939">
    <div class="subject"><a href="#post-84939">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">I not realy programed this by my self !<br /><br />You sure know the code example killproc.<br /><br />I start a prog with the funktion Shellexecute and the program i started I want to terminate.<br />But, i doesnt work.<br /><br />further I want to prgramm a taskmanager. But a manager which isnt able to terminate a programm is not that useful ! :grin: :grin: <br /><br />Thanks for your attention ! I still need help.</div>
    <div class="meta">Posted on 2003-03-04 07:17:15 by Forginforcer</div>
   </div>
   <div class="post" id="post-84942">
    <div class="subject"><a href="#post-84942">hmmmmm</a></div>
    <div class="body">You should check my Program. Hit www Button at this post.<br />invoke SendMessage,hWndtgt,WM_CLOSE,NULL</div>
    <div class="meta">Posted on 2003-03-04 07:22:17 by realvampire</div>
   </div>
   <div class="post" id="post-84953">
    <div class="subject"><a href="#post-84953">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">Can you copy the terminate funktion for me ?<br />Thanks for your attention !</div>
    <div class="meta">Posted on 2003-03-04 07:53:42 by Forginforcer</div>
   </div>
   <div class="post" id="post-84956">
    <div class="subject"><a href="#post-84956">....</a></div>
    <div class="body">Im already copy it.<br /><br /><pre><code><br /><br />invoke SendMessage,hWnd,WM_TERMINATE,NULL<br /><br /></code></pre></div>
    <div class="meta">Posted on 2003-03-04 07:57:47 by realvampire</div>
   </div>
   <div class="post" id="post-84958">
    <div class="subject"><a href="#post-84958">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">shit, then i have to get the handle !<br />Its works something with findwindow...<br />but i will try !</div>
    <div class="meta">Posted on 2003-03-04 07:59:33 by Forginforcer</div>
   </div>
   <div class="post" id="post-85013">
    <div class="subject"><a href="#post-85013">huhu !</a></div>
    <div class="body">do someone knows a rigid methode, to terminate a process ? If a prog crashed, it cant follow the close instruktion...</div>
    <div class="meta">Posted on 2003-03-04 12:58:18 by Forginforcer</div>
   </div>
   <div class="post" id="post-85091">
    <div class="subject"><a href="#post-85091">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">Heya..<br />When I did this I too went for the WM_CLOSE approach at first.<br />Later I realized that I would be better off spoofing user input to the real process manager, making it appear as if I had actually killed the process with the operating system's manager.<br />I didn't try to make it work on every version of Windows, it was a means to an end for a personal tool.<br />For a more &quot;professional&quot; method of closing applications, I seem to recall that was possible via a couple of other methods... one was a rundll invocation, the others I can't remember, only that I saw them in malicious source which I'd rather not elaborate on... <br />cya.</div>
    <div class="meta">Posted on 2003-03-04 19:42:34 by Homer</div>
   </div>
   <div class="post" id="post-85127">
    <div class="subject"><a href="#post-85127">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">forget ShellExecute, use CreateProcess. That way you get a proper process handle etc. Second, starting with WM_CLOSE or DestroyWindow is a good idea - if app hasn't closed after some timeout period, you can use TerminateProcess. But you should always give it a chance to terminate cleanly first, as TerminateProcess doesn't decrement DLL reference count...</div>
    <div class="meta">Posted on 2003-03-05 02:09:16 by f0dder</div>
   </div>
   <div class="post" id="post-85273">
    <div class="subject"><a href="#post-85273">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">I made a simple task manager that used terminate process, then I got silly... I managed to trim it down to 3.5k!<br /><br />If you can understand it, here is the code. I did a lot of things to get the bytes down so it fits in 3.5k (even though it doesn't matter, its the principal of the thing :) ).<br /><br /><pre><code><br />.386<br />.model flat, stdcall<br />option casemap&#58;none<br />.nolist<br /><br />include \masm32\include\no_imp\windows.inc<br />include \masm32\include\no_imp\kernel32.inc<br />include \masm32\include\no_imp\user32.inc<br />includelib \masm32\lib\kernel32.lib<br />includelib \masm32\lib\user32.lib<br /><br />ListProc proto &#58;DWORD,&#58;DWORD,&#58;DWORD,&#58;DWORD<br /><br />.const<br />CM_EXIT     equ     101<br />CM_REFRESH  equ     102<br />CM_ABOUT    equ     103<br /><br />CM_IDLE     equ     1040h<br />CM_NORM     equ     1020h<br />CM_HIGH     equ     1080h<br />CM_REAL     equ     1100h<br /><br />CM_KILL     equ     0999<br /><br />.listall<br /><br />.data?<br />hSnapshot   dd              ?<br />hList       dd              ?<br />OldWndProc  dd              ?<br />hMenu       dd              ?<br />uProcess    PROCESSENTRY32  &lt;&gt;<br />myRect      RECT            &lt;&gt;<br />wc          WNDCLASSEX      &lt;&gt;<br />msg         MSG             &lt;&gt;<br /><br />.code<br />data&#58;<br />ClassName   db      &quot;FEC&quot;,0<br />AppName     db      &quot;STM v.4&quot;,0<br />LBClass     db      &quot;LISTBOX&quot;,0<br />AbtTitle    db      &quot;About..&quot;, 0<br />AbtText     db      &quot;Simple Task Manager - by Mirno.&quot;, 13, 10<br />            db      &quot;email&#58; &#91;email&#93;mirno@fsmail.net&#91;/email&#93;&quot;, 0<br />MsgText     db      &quot;Couldnt change settings&quot;, 0<br /><br />MTitle      db      &quot;File&quot;,0<br />M1          db      &quot;Refresh&quot;,0<br />M2          db      &quot;About&quot;,0<br />M3          db      &quot;Exit&quot;,0<br /><br />P1          db      &quot;Idle&quot;,0<br />P2          db      &quot;Norm&quot;,0<br />P3          db      &quot;High&quot;,0<br />P4          db      &quot;Real&quot;,0<br />P5          db      &quot;Kill&quot;,0<br /><br />start&#58;<br />    xor eax, eax<br />    mov wc.cbClsExtra, eax<br />    mov wc.cbWndExtra, eax<br />    mov wc.lpszMenuName, eax<br /><br />    invoke LoadCursor, eax, IDC_ARROW<br />    mov wc.hCursor, eax<br />    mov wc.cbSize, SIZEOF WNDCLASSEX<br />    mov wc.style, CS_HREDRAW or CS_VREDRAW<br />    mov wc.lpfnWndProc, OffSet MainProc<br />    mov wc.hbrBackground, COLOR_BTNFACE + 1<br />    mov wc.lpszClassName, OffSet ClassName<br /><br />    invoke GetModuleHandle, NULL<br />    mov wc.hInstance, eax<br /><br />    invoke LoadIcon, eax, 1<br />    mov wc.hIcon, eax<br />    mov wc.hIconSm, eax<br /><br />    invoke RegisterClassEx, ADDR wc<br /><br />    invoke CreateMenu<br />    mov hMenu, eax<br />    xor edx, edx<br /><br />    push OFFSET M3<br />    push CM_EXIT<br />    push edx<br />    push eax<br /><br />    push edx<br />    push edx<br />    push MF_SEPARATOR<br />    push eax<br /><br />    push OFFSET M2<br />    push CM_ABOUT<br />    push edx<br />    push eax<br /><br />    push OFFSET M1<br />    push CM_REFRESH<br />    push edx<br />    push eax<br />    call AppendMenu<br />    call AppendMenu<br />    call AppendMenu<br />    call AppendMenu<br /><br />    invoke CreateMenu<br />    push eax<br /><br />    invoke AppendMenu, eax, MF_STRING or MF_POPUP, hMenu, ADDR MTitle<br /><br />    invoke CreatePopupMenu<br />    mov hMenu, eax<br />    xor edx, edx<br /><br />    push OFFSET P5<br />    push CM_KILL<br />    push edx<br />    push eax<br /><br />    push edx<br />    push edx<br />    push MF_SEPARATOR<br />    push eax<br /><br />    push OFFSET P4<br />    push CM_REAL<br />    push edx<br />    push eax<br /><br />    push OFFSET P3<br />    push CM_HIGH<br />    push edx<br />    push eax<br /><br />    push OFFSET P2<br />    push CM_NORM<br />    push edx<br />    push eax<br /><br />    push OFFSET P1<br />    push CM_IDLE<br />    push edx<br />    push eax<br /><br />    call AppendMenu<br />    call AppendMenu<br />    call AppendMenu<br />    call AppendMenu<br />    call AppendMenu<br />    call AppendMenu<br /><br />    pop eax<br />    invoke CreateWindowEx, NULL, ADDR ClassName, ADDR AppName, \<br />        WS_OVERLAPPEDWINDOW or WS_CLIPCHILDREN, CW_USEDEFAULT, \<br />        CW_USEDEFAULT, 300, 300, NULL, eax, \<br />        wc.hInstance, NULL<br />    push eax<br /><br />    invoke ShowWindow, eax, SW_SHOWNORMAL<br />    call UpdateWindow<br />    .WHILE TRUE<br />        invoke GetMessage, ADDR msg,NULL,0,0<br />        .BREAK .IF &#40;!eax&#41;<br />        invoke TranslateMessage, ADDR msg<br />        invoke DispatchMessage, ADDR msg<br />    .ENDW<br /><br />    invoke	ExitProcess, msg.wParam<br /><br /><br />MainProc proc hWnd&#58;HWND, uMsg&#58;UINT, wParam&#58;WPARAM, lParam&#58;LPARAM<br />    mov eax, uMsg<br />    cmp eax, WM_CREATE<br />    jne Not_CREATE<br />        invoke CreateWindowEx, WS_EX_CLIENTEDGE,\<br />            ADDR LBClass, NULL,\<br />            WS_CHILD or WS_VISIBLE or WS_VSCROLL or \<br />            LBS_HASSTRINGS or LBS_NOINTEGRALHEIGHT,\<br />            0, 0, 0, 0,\<br />            hWnd, NULL, wc.hInstance, NULL<br />        mov	hList, eax<br /><br />        invoke SetWindowLong, eax, GWL_WNDPROC, ADDR ListProc<br />        mov OldWndProc,eax<br /><br />        jmp Refresh<br /><br />Not_CREATE&#58;<br />    cmp eax, WM_COMMAND<br />    jne Not_COMMAND<br />        mov eax, wParam<br />        and eax, 0FFFFh<br /><br />        cmp eax, CM_REFRESH<br />        je Refresh<br /><br />        cmp eax, CM_EXIT<br />        jne @F<br />            invoke DefWindowProc, hWnd, WM_CLOSE, 0, 0<br />            jmp Main_end<br />@@&#58;<br />        cmp eax, CM_ABOUT<br />        jne Main_end<br />            invoke MessageBox, hWnd, ADDR AbtText, ADDR AbtTitle, MB_OK<br />            jmp Main_end<br /><br />Not_COMMAND&#58;<br />    cmp eax, WM_SIZE<br />    jne Not_SIZE<br />        mov edx, lParam<br />        mov eax, lParam<br />        shr edx, 16<br />        and eax, 0FFFFh<br /><br />        invoke MoveWindow, hList, 0, 0, eax, edx, TRUE<br />        ;Resize the edit box to fill the whole window<br />        jmp Main_end<br /><br />Not_SIZE&#58;<br />    cmp eax, WM_DESTROY<br />    jne Not_DESTROY<br />        invoke PostQuitMessage,NULL<br /><br />Not_DESTROY&#58;<br />        invoke DefWindowProc,hWnd,eax,wParam,lParam<br /><br />Main_end&#58;<br />    ret<br />MainProc endp<br /><br />ListProc proc hWnd&#58;HWND, uMsg&#58;UINT, wParam&#58;WPARAM, lParam&#58;LPARAM<br />    mov eax, uMsg<br /><br />    cmp eax, WM_RBUTTONDOWN<br />    jne Not_RBUTTONDOWN<br />        invoke CallWindowProc, OldWndProc, hList, WM_LBUTTONDOWN, wParam, lParam<br />        jmp List_end<br /><br />Not_RBUTTONDOWN&#58;<br />    cmp eax, WM_RBUTTONUP<br />    jne Not_RBUTTONUP<br />        invoke CallWindowProc, OldWndProc, hList, WM_LBUTTONUP, wParam, lParam<br /><br />        invoke SendMessage, hList, LB_GETCURSEL, 0, 0<br />        cmp eax, LB_ERR<br />        je List_end<br />        invoke SendMessage, hList, LB_GETITEMDATA, eax, 0<br />        invoke OpenProcess, PROCESS_QUERY_INFORMATION, FALSE, eax<br />        invoke GetPriorityClass, eax<br /><br />        or eax, 1000h<br />        push eax<br />        invoke CheckMenuItem, hMenu, eax, MF_BYCOMMAND or MF_CHECKED<br /><br />        invoke GetWindowRect, hList, ADDR myRect<br />        mov eax, lParam<br />        mov edx, lParam + 2<br />        and eax, 0FFFFh<br />        and edx, 0FFFFh<br />        add eax, myRect.left<br />        add edx, myRect.top<br />        invoke TrackPopupMenuEx, hMenu, TPM_HORIZONTAL, \<br />                eax, edx, hWnd, NULL<br /><br />        pop ecx<br />        invoke CheckMenuItem, hMenu, ecx, MF_BYCOMMAND or MF_UNCHECKED<br />        jmp List_end<br /><br />Not_RBUTTONUP&#58;<br />    cmp eax, WM_COMMAND<br />    jne Not_COMMAND<br />        mov eax, wParam<br />        .IF ax == CM_KILL<br />            invoke SendMessage, hList, LB_GETCURSEL, 0, 0<br />            invoke SendMessage, hList, LB_GETITEMDATA, eax, 0<br />            invoke OpenProcess, PROCESS_TERMINATE, 1, eax<br />            invoke TerminateProcess, eax, 0<br /><br />            invoke Sleep, 500<br /><br />        Refresh&#58;&#58;<br />            invoke SendMessage, hList, LB_RESETCONTENT, 0, 0<br /><br />            mov uProcess.dwSize, sizeof uProcess<br />            invoke CreateToolhelp32Snapshot, TH32CS_SNAPPROCESS, 0<br />            mov hSnapshot, eax<br />            invoke Process32First, eax, ADDR uProcess<br /><br />            .while eax<br />                invoke SendMessage, hList, LB_ADDSTRING, 0, ADDR uProcess.szExeFile<br />                invoke SendMessage, hList, LB_SETITEMDATA, eax, uProcess.th32ProcessID<br />                invoke Process32Next, hSnapshot, ADDR uProcess<br />            .endw<br /><br />            invoke CloseHandle, hSnapshot<br /><br />        .ELSE<br />            invoke SendMessage, hList, LB_GETCURSEL, 0, 0<br />            invoke SendMessage, hList, LB_GETITEMDATA, eax, 0<br />            invoke OpenProcess, PROCESS_SET_INFORMATION, FALSE, eax<br />            mov edx, wParam<br />            and edx, 0FFFh<br />            invoke SetPriorityClass, eax, edx<br /><br />            .IF !eax<br />                invoke MessageBox, hWnd, ADDR MsgText, NULL, MB_ICONEXCLAMATION or MB_OK<br />            .ENDIF<br />        .ENDIF<br />        jmp List_end<br /><br />Not_COMMAND&#58;<br />        invoke CallWindowProc, OldWndProc, hList, eax, wParam, lParam<br />        ret<br /><br />List_end&#58;<br />    xor eax,eax<br />    ret<br />ListProc endp<br /><br />end start<br /></code></pre><br /><br />Things like pushing all the arguments in a row, so that eax is unmodified etc. just to save about 2 bytes each time! It got ridiculous, and I finally reached the lean 3.5k mark using all this, AND the no-jump table include files.<br /><br />Mirno</div>
    <div class="meta">Posted on 2003-03-05 15:20:38 by Mirno</div>
   </div>
   <div class="post" id="post-85330">
    <div class="subject"><a href="#post-85330">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">Hi How r u?<br /><br />I hope this will work with u<br /><br />Get the handle for the process u want to terminate using OpenProcess API Function with access right terminate the call TerminateProcess it will work very good<br /><br />to enumrate all the processess in the system call either EnumProcesses or user CreateToolhelpSNapshot in Toolhlp.h<br /><br /><br />If u need more information about these function look in the MSDN<br /><br />please fill free to mail me if u want anything else</div>
    <div class="meta">Posted on 2003-03-06 01:24:36 by Hisham</div>
   </div>
   <div class="post" id="post-85582">
    <div class="subject"><a href="#post-85582">Probs whith terminating a Prog, how to terminate them ?</a></div>
    <div class="body">hiiiiiiiiiiiii thanks alot !<br /><br />I just see, that you hav answered my questen !<br />I didnt get a email notification.<br /><br />I will test it immediately !</div>
    <div class="meta">Posted on 2003-03-07 08:19:51 by Forginforcer</div>
   </div>
  </div>
 </body>
</html>