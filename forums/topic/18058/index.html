<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>2 Windows: Why can't i close them and terminate the program? - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=18058" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=3">MAIN</a> &raquo; <a href="../?id=18058">2 Windows: Why can't i close them and terminate the program?</a></p>
   <div class="post" id="post-139437">
    <div class="subject"><a href="#post-139437">2 Windows: Why can't i close them and terminate the program?</a></div>
    <div class="body">Hi Everyone,<br /><br /><br />This small program of mine creates another window with its own WndProc. I'm trying to close both windows and terminate the program. <br />To close the second window i created, i used the CloseTwo method declared in window2.asm and it invokes DestroyWindow.<br />Although i can close both windows, the program does not get terminated. I need to go go to the Task Manager to end the process manually. Where did i go wrong?<br /><br />window1.asm code:<br />-------------------------------------------------------------------------------------------------<br />include...<br />includelib...<br /><br />include	window2.asm<br /><br />WinMain			PROTO:DWORD,:DWORD,:DWORD,:DWORD<br /><br />.data<br />ClassName       DB  		&quot;Window 1&quot;,0<br />AppName         DB  		&quot;Window 1&quot;,0<br />MenuName       DB		&quot;WindowsMenu&quot;,0<br /><br />.data?<br />hInstance         HINSTANCE	?<br />CommandLine   LPSTR	?<br /><br />wwidth            DWORD	?<br />wheight           DWORD	?<br /><br />twoCreated     DWORD	?<br /><br />.const<br />IDM_CREATE	EQU	1<br /><br />.code<br />start:<br />    invoke 	GetModuleHandle, NULL<br />    mov    	hInstance,eax<br /><br />    invoke	GetCommandLine<br />    mov    	CommandLine,eax<br /><br />    invoke 	WinMain, hInstance,NULL,CommandLine, SW_SHOWDEFAULT<br />    invoke 	ExitProcess,eax<br /><br />;	####################  WinMain  ####################<br />    WinMain proc hInst:HINSTANCE,hPrevInst:HINSTANCE,CmdLine:LPSTR,CmdShow:DWORD<br />        LOCAL	wc		:WNDCLASSEX<br />        LOCAL	msg		:MSG<br />        LOCAL	hwnd	:HWND<br /><br />        mov		wc.cbSize,	SIZEOF WNDCLASSEX<br />        mov		wc.style, 		CS_HREDRAW or CS_VREDRAW<br />        mov		wc.lpfnWndProc, 	OFFSET WndProc<br />        mov		wc.cbClsExtra,	NULL<br />        mov		wc.cbWndExtra,	NULL<br />        push 	hInstance<br />        pop		wc.hInstance<br />        mov		wc.hbrBackground,	COLOR_BTNFACE+1<br />		mov		wc.lpszMenuName,	OFFSET MenuName<br />        mov		wc.lpszClassName,	OFFSET ClassName<br /><br />        invoke	LoadIcon,NULL,IDI_APPLICATION<br />        mov		wc.hIcon,		eax<br />        mov		wc.hIconSm,	eax<br /><br />		invoke	LoadCursor,NULL,IDC_ARROW<br />        mov		wc.hCursor,	eax<br /><br />        invoke	RegisterClassEx,ADDR wc<br /><br />        mov		wwidth, 380<br />        mov		wheight, 110<br /><br />        INVOKE	CreateWindowEx,NULL,ADDR ClassName,<br />                                ADDR  AppName,WS_OVERLAPPEDWINDOW,CW_USEDEFAULT,CW_USEDEFAULT,<br />                                wwidth,wheight,NULL,NULL,hInst,NULL<br />        mov  	hwnd,eax<br /><br />        invoke	ShowWindow, hwnd,SW_SHOWNORMAL<br />        invoke	UpdateWindow, hwnd<br /><br />        .while	TRUE<br />            invoke	GetMessage, ADDR msg,NULL,0,0<br />            .break .if (!eax)<br />            invoke	TranslateMessage, ADDR msg<br />            invoke	DispatchMessage, ADDR msg<br />        .endw<br /><br />         mov 	eax,FALSE<br />         mov	                twoCreated,eax<br /><br />        mov		eax,msg.wParam<br />        ret<br />    WinMain endp<br /><br />;	####################  WndProc  ####################<br />    WndProc 	proc uses ebx ecx edx esi edi,hWnd:HWND,uMsg:UINT,wParam:WPARAM,lParam:LPARAM<br /><br />        .if uMsg==WM_CREATE<br /><br />        .elseif uMsg==WM_COMMAND<br />             mov	eax,wParam<br />            .if ax==IDM_CREATE<br />	mov	eax,TRUE<br />	mov	twoCreated,eax<br />	invoke	CreateTwo,hWnd,hInstance<br />            .endif<br /><br />        .elseif uMsg==WM_DESTROY<br />	mov	eax,twoCreated<br />	.if eax==TRUE<br />                    invoke	CloseTwo<br />	.endif<br />	mov	twoCreated,FALSE<br /><br />	invoke	PostQuitMessage,NULL<br />        .else<br />	invoke	DefWindowProc,hWnd,uMsg,wParam,lParam		<br />	ret<br />        .endif<br /><br />        ret<br />    WndProc endp<br /><br />end start<br /><br /><br />window2.asm code:<br />-------------------------------------------------------------------------------------------------<br />CloseTwo		PROTO<br /><br />.data<br />ClassName2       	DB  	&quot;Window 2&quot;,0<br />AppName2         	DB  	&quot;Window 2&quot;,0<br /><br />.data?<br />h_Win2		HWND	?<br /><br />.code<br />;	#################### CreateTwo ####################<br />CreateTwo  proc hWin:HWND,hInstance:DWORD<br />    LOCAL	wc2			:WNDCLASSEX<br />    LOCAL	msg			:MSG<br /><br />    mov		wc2.cbSize,	SIZEOF WNDCLASSEX<br />    mov		wc2.style, 	CS_HREDRAW or CS_VREDRAW<br />    mov		wc2.lpfnWndProc, 	OFFSET WndProc2<br />    mov		wc2.cbClsExtra,	NULL<br />    mov		wc2.cbWndExtra,	NULL<br />    push	hInstance<br />    pop		wc2.hInstance<br />    mov		wc2.hbrBackground,COLOR_BTNFACE+1<br />    mov		wc2.lpszMenuName,NULL<br />    mov		wc2.lpszClassName,OFFSET ClassName2<br /><br />    invoke	LoadIcon,NULL,IDI_APPLICATION<br />    mov		wc2.hIcon,	eax<br />    mov		wc2.hIconSm,	eax<br />    invoke	LoadCursor,NULL,IDC_ARROW<br />    mov		wc2.hCursor,	eax<br /><br />    invoke	RegisterClassEx,ADDR wc2<br /><br />    invoke	CreateWindowEx,NULL,ADDR ClassName2,<br />                ADDR  AppName2,WS_OVERLAPPEDWINDOW,CW_USEDEFAULT,CW_USEDEFAULT,<br />                280,110,NULL,NULL,hInstance,NULL<br />    mov  	h_Win2,eax<br /><br />    invoke	ShowWindow,h_Win2,SW_SHOWNORMAL<br />    invoke	UpdateWindow,h_Win2<br /><br />    .while	TRUE<br />        invoke	GetMessage, ADDR msg,NULL,0,0<br />        .break .if (!eax)<br />            invoke	TranslateMessage, ADDR msg<br />            invoke	DispatchMessage, ADDR msg<br />        .endw<br />	<br />        mov		eax,msg.wParam<br />        ret<br />CreateTwo  endp<br /><br />;	####################  WndProc2  ####################<br />    WndProc2	proc uses ebx ecx edx esi edi,hWin:HWND,uMsg:UINT,wParam:WPARAM,lParam:LPARAM<br /><br />        .if	uMsg==WM_CREATE<br />        .elseif uMsg==WM_COMMAND<br />        .elseif uMsg==WM_DESTROY<br />  	invoke	PostQuitMessage,NULL<br />        .else<br />	invoke	DefWindowProc,hWin,uMsg,wParam,lParam		<br />	ret<br />        .endif<br /><br />        ret<br />    WndProc2 endp<br /><br />;	####################  CloseTwo  ####################<br />    CloseTwo    proc uses eax<br />        invoke	DestroyWindow,h_Win2<br />        ret<br />    CloseTwo	endp<br /><br />-----------------------<br /><br />Thanks.</div>
    <div class="meta">Posted on 2004-04-19 07:21:52 by trexxz</div>
   </div>
   <div class="post" id="post-139440">
    <div class="subject"><a href="#post-139440">2 Windows: Why can't i close them and terminate the program?</a></div>
    <div class="body">Although you create 2 windows the message loop has to be only one. In CreateTwo procedure you don't need the .while .endw block. The main message loop will catch messages for both windows and call WndProc or WndProc2 accordingly.</div>
    <div class="meta">Posted on 2004-04-19 07:40:48 by Vaxon</div>
   </div>
   <div class="post" id="post-139507">
    <div class="subject"><a href="#post-139507">2 Windows: Why can't i close them and terminate the program?</a></div>
    <div class="body">Thanks Vaxon!<br /><br />Now i can close both window 1 and 2 when i close 1. But now i can't keep window 1 open when i close window 2...<br />How do i keep window 1 open although window 2 is closed?</div>
    <div class="meta">Posted on 2004-04-19 20:45:58 by trexxz</div>
   </div>
   <div class="post" id="post-139508">
    <div class="subject"><a href="#post-139508">2 Windows: Why can't i close them and terminate the program?</a></div>
    <div class="body">Does it mean that i need 2 message loops?</div>
    <div class="meta">Posted on 2004-04-19 20:48:38 by trexxz</div>
   </div>
   <div class="post" id="post-139514">
    <div class="subject"><a href="#post-139514">2 Windows: Why can't i close them and terminate the program?</a></div>
    <div class="body">If you don't want the program to quit when you close window 2, don't call PostQuitMessage in window 2.</div>
    <div class="meta">Posted on 2004-04-19 22:02:09 by tenkey</div>
   </div>
   <div class="post" id="post-139520">
    <div class="subject"><a href="#post-139520">2 Windows: Why can't i close them and terminate the program?</a></div>
    <div class="body"><div class="quote"><br />How do i keep window 1 open although window 2 is closed? </div><br /><br />You can handle the WM_CLOSE of win2, no close the win2 but hide the win2.<br /><pre><code><br />        .ELSEIF uMsg == WM_CLOSE<br />            ;-------------------------<br />            ; Hide the win2 when user close the win2<br />            ;-------------------------<br />            invoke ShowWindow, hWnd, SW_HIDE<br /></code></pre><br /><br />and close the program  when user close win1.<br /><br />please take a look into the attached archive.</div>
    <div class="meta">Posted on 2004-04-19 22:36:26 by purpleendurer</div>
   </div>
   <div class="post" id="post-139523">
    <div class="subject"><a href="#post-139523">2 Windows: Why can't i close them and terminate the program?</a></div>
    <div class="body">Got it!<br /><br />Thank you tenkey and purpleendurer. :grin:</div>
    <div class="meta">Posted on 2004-04-20 00:28:09 by trexxz</div>
   </div>
  </div>
 </body>
</html>