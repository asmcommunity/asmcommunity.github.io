<!DOCTYPE html>
<html>
 <head>
  <meta charset="utf-8" />
  <title>how do i get a new disk insertion notification? - Forums - ASM Community</title>
  <link rel="stylesheet" type="text/css" href="../../../style.css" />
  <link rel="canonical" href="../?id=20137" />
     </head>
 <body>
  <div id="header">
   <h1><a href="../../../">ASM Community</a></h1>
  </div>
  <div id="content">
   <p class="breadcrumbs"><a href="../../../">Home</a> &raquo; <a href="../../">Forums</a> &raquo; <a href="../../board/?id=3">MAIN</a> &raquo; <a href="../?id=20137">how do i get a new disk insertion notification?</a></p>
   <div class="post" id="post-154248">
    <div class="subject"><a href="#post-154248">how do i get a new disk insertion notification?</a></div>
    <div class="body">I want to get notified when the user inserts a new disk, I'm thinking specially of USB sticks and things like that. I know the FindFirstChangeNotification API, but the MSDN doesn't tell me how I should check for new drives.<br /><br />The only thing I can think of is polling every n seconds, but I don't think that that'll be the way to go.<br /><br />Thanks in advance!</div>
    <div class="meta">Posted on 2004-12-16 10:19:52 by lifewire</div>
   </div>
   <div class="post" id="post-154442">
    <div class="subject"><a href="#post-154442">how do i get a new disk insertion notification?</a></div>
    <div class="body">Hi<pre><code>.386<br />.model flat, stdcall<br />option casemap &#58;none<br /><br />include windows.inc<br />include user32.inc<br />include kernel32.inc<br />include gdi32.inc<br /><br />includelib user32.lib<br />includelib kernel32.lib<br />includelib gdi32.lib<br /><br />DBT_DEVICEARRIVAL        = 8000h<br />DBT_DEVICEREMOVECOMPLETE = 8004h<br />DBT_DEVTYP_VOLUME        = 2<br /><br />DEV_BROADCAST_HDR struc<br />  dbch_size       DWORD ?<br />  dbch_devicetype DWORD ?<br />  dbch_reserved   DWORD ?<br />DEV_BROADCAST_HDR ends<br /><br />DEV_BROADCAST_VOLUME struc<br />  dbcv_size       DWORD ?<br />  dbcv_devicetype DWORD ?<br />  dbcv_reserved   DWORD ?<br />  dbcv_unitmask   DWORD ?<br />  dbcv_flags      WORD  ?<br />                  WORD  ? ;  __alignmentDummy<br />DEV_BROADCAST_VOLUME ends<br /><br />.const<br />  szClsName db 'p2mDiskArriveRemoveDetectClass',0<br />  szAppName db 'Disk arrive/remove detect',0<br />  szArrived db 'Arrived', 0<br />  szRemoved db 'Removed', 0<br /><br />.code<br /><br />;---------------------------------------------------------;<br />  align 4<br />  DriveFromMask proc dwMask&#58;DWORD<br />    mov ecx,dwMask<br />    xor eax,eax<br />    mov al,'A'<br />    .while &#40;al &lt;= 'Z'&#41; &amp;&amp; &#40;ecx != 0&#41;<br />      .break .if &#40;ecx &amp; 1&#41;<br />      shr ecx,1<br />      inc al<br />    .endw<br />    .if al &gt; 'Z'<br />      xor al,al<br />    .else<br />      mov ah,'&#58;'<br />    .endif<br />    ret<br />  DriveFromMask endp    <br /><br />;---------------------------------------------------------;<br />  align 4<br />  wndproc proc uses ebx edi esi, hWnd&#58;HWND, uMsg&#58;UINT, wParam&#58;WPARAM, lParam&#58;LPARAM<br />    local buf&#58;dword<br /><br />    .if uMsg == WM_DEVICECHANGE<br />      mov eax,lParam<br />      .if eax != 0<br />        mov eax,&#40;DEV_BROADCAST_HDR ptr &#91;eax&#93;&#41;.dbch_devicetype<br />        .if  &#40;wParam == DBT_DEVICEARRIVAL&#41; &amp;&amp; &#40;eax == DBT_DEVTYP_VOLUME&#41;<br />          mov eax,lParam<br />          invoke DriveFromMask, &#40;DEV_BROADCAST_VOLUME ptr &#91;eax&#93;&#41;.dbcv_unitmask<br />          .if eax != 0<br />            mov buf,eax<br />            invoke MessageBox, hWnd, addr buf, offset szArrived, MB_OK<br />          .endif<br />        .elseif &#40;wParam == DBT_DEVICEREMOVECOMPLETE&#41; &amp;&amp; &#40;eax == DBT_DEVTYP_VOLUME&#41;<br />          mov eax,lParam<br />          invoke DriveFromMask, &#40;DEV_BROADCAST_VOLUME ptr &#91;eax&#93;&#41;.dbcv_unitmask<br />          .if eax != 0<br />            mov buf,eax<br />            invoke MessageBox, hWnd, addr buf, offset szRemoved, MB_OK<br />          .endif<br />        .endif<br />      .endif<br />      invoke DefWindowProc, hWnd, uMsg, wParam, lParam<br /><br />    .elseif uMsg == WM_DESTROY<br />      invoke PostQuitMessage, NULL<br />      xor eax,eax<br /><br />    .else<br />      invoke DefWindowProc, hWnd, uMsg, wParam, lParam<br />    .endif<br /><br />    ret<br />  wndproc endp<br /><br />;---------------------------------------------------------;<br />  align 4<br />  main proc<br />    local wc&#58;WNDCLASS<br />    local msg&#58;MSG<br /><br />    invoke RtlZeroMemory, addr wc, sizeof wc<br /><br />    mov wc.style,CS_HREDRAW or CS_VREDRAW<br />    mov wc.lpfnWndProc,offset wndproc<br />    invoke GetModuleHandle, NULL<br />    mov wc.hInstance,eax<br />    invoke LoadIcon, NULL, IDI_APPLICATION<br />    mov wc.hIcon,eax<br />    invoke LoadCursor, NULL, IDC_ARROW<br />    mov wc.hCursor,eax<br />    mov wc.hbrBackground,COLOR_APPWORKSPACE+1<br />    mov wc.lpszClassName,offset szClsName<br /><br />    invoke RegisterClass, addr wc<br />    .if eax != 0<br /><br />      invoke CreateWindowEx, NULL, addr szClsName, addr szAppName,\<br />             WS_OVERLAPPEDWINDOW,<br />             10, 10, 250, 80,\<br />             NULL, NULL, wc.hInstance, NULL<br />      .if eax != NULL<br /><br />        push eax<br />        push SW_SHOWNORMAL<br />        push eax<br />        call ShowWindow<br />        call UpdateWindow<br /><br />        .while TRUE<br />          invoke GetMessage, addr msg, NULL, 0, 0<br />          .break .if &#40;!eax&#41;<br />          invoke TranslateMessage, addr msg<br />          invoke DispatchMessage, addr msg<br />        .endw<br />      .endif<br />    .endif<br /><br />    ret<br />  main endp<br /><br />;---------------------------------------------------------;<br />  align 4<br />  start&#58;<br />    invoke main  <br />    invoke ExitProcess, 0<br /><br />end start</code></pre>For detect USB or not USB need use <em>DeviceIoControl + IOCTL_STORAGE_QUERY_PROPERTY</em>. If need i post code later.</div>
    <div class="meta">Posted on 2004-12-19 22:45:30 by P2M</div>
   </div>
   <div class="post" id="post-154471">
    <div class="subject"><a href="#post-154471">how do i get a new disk insertion notification?</a></div>
    <div class="body">great, thanks! i didn't know about the WM_DEVICECHANGE, very interesting!</div>
    <div class="meta">Posted on 2004-12-20 07:34:26 by lifewire</div>
   </div>
  </div>
 </body>
</html>